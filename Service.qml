import QtQuick
import Quickshell
import Quickshell.Io
import Quickshell.Services.Pam
import Quickshell.Wayland
import qs.Commons
import "Designs.js" as Designs
import "DisplayPower.js" as DisplayPower
import "Bridge.js" as Bridge

Item {
  id: root

  property var shell: null
  property var manifest: null
  property string omarchyPath: ""

  LocalSettings { id: localSettings; pluginId: root.pluginId }
  readonly property var settingsConfig: shell && shell.shellConfig
    ? shell.shellConfig : localSettings.config

  // selected design lives on this plugin's entry in shell.json
  readonly property string pluginId: manifest && manifest.id ? String(manifest.id) : "io.github.sirjul1337.lock-explorer"
  property string designOverride: ""
  readonly property string configuredDesignId: {
    var cfg = root.settingsConfig
    var list = cfg && Array.isArray(cfg.plugins) ? cfg.plugins : []
    for (var i = 0; i < list.length; i++) {
      var entry = list[i]
      if (entry && String(entry.id || "") === pluginId && entry.design) return String(entry.design)
    }
    return Designs.DEFAULT_ID
  }
  readonly property string designId: designOverride.length > 0 ? designOverride : configuredDesignId
  readonly property bool designHasClip: {
    var r = designsRevision
    var d = Designs.byId(designId)
    return !!(d && d.clip)
  }
  // The video file behind the current clip design; user ClipDesigns get their
  // clipFile filled in by the lock-designs scan.
  readonly property string designClipPath: {
    var r = designsRevision
    var d = Designs.byId(designId)
    if (!d || !d.clip || !d.clipFile) return ""
    return home + "/.config/omarchy/lock-videos/" + d.clipFile
  }

  // "all" or an output name (see `omarchy-shell lock monitors`). Other
  // monitors get the companion screen.
  property string inputMonitorOverride: ""
  readonly property string configuredInputMonitor: {
    var cfg = root.settingsConfig
    var list = cfg && Array.isArray(cfg.plugins) ? cfg.plugins : []
    for (var i = 0; i < list.length; i++) {
      var entry = list[i]
      if (entry && String(entry.id || "") === pluginId && entry.inputMonitor) return String(entry.inputMonitor)
    }
    return "all"
  }
  readonly property string inputMonitor: inputMonitorOverride.length > 0 ? inputMonitorOverride : configuredInputMonitor

  // How the lock screen leaves the screen when the password checks out. Off by
  // default, the unlock stays instant until it is turned on. Saved on the
  // plugin entry as `unlock` and `unlockMs`.
  readonly property var unlockAnimations: ["fade", "zoom", "rise", "none"]
  readonly property int defaultUnlockDuration: 400
  property string unlockOverride: ""
  property int unlockDurationOverride: -1
  readonly property string configuredUnlock: {
    var cfg = root.settingsConfig
    var list = cfg && Array.isArray(cfg.plugins) ? cfg.plugins : []
    for (var i = 0; i < list.length; i++) {
      var entry = list[i]
      if (entry && String(entry.id || "") === pluginId && entry.unlock) return String(entry.unlock)
    }
    return "none"
  }
  readonly property int configuredUnlockDuration: {
    var cfg = root.settingsConfig
    var list = cfg && Array.isArray(cfg.plugins) ? cfg.plugins : []
    for (var i = 0; i < list.length; i++) {
      var entry = list[i]
      if (entry && String(entry.id || "") === pluginId && entry.unlockMs !== undefined)
        return Math.max(0, Math.min(2000, Number(entry.unlockMs) || 0))
    }
    return defaultUnlockDuration
  }
  readonly property string unlockAnimation: {
    var value = unlockOverride.length > 0 ? unlockOverride : configuredUnlock
    return unlockAnimations.indexOf(value) === -1 ? "none" : value
  }
  readonly property int unlockDuration: unlockDurationOverride >= 0 ? unlockDurationOverride : configuredUnlockDuration
  readonly property bool unlockAnimated: unlockAnimation !== "none" && unlockDuration > 0

  // How long the unlock screen stays lit before the display is blanked. The
  // session is locked before every suspend, so on a machine that sleeps this
  // delay is what the user sees on resume: too short and the screen goes dark
  // before there is time to type. Saved on the plugin entry as `blankMs`.
  readonly property int defaultBlankDelay: 5000
  property int blankDelayOverride: -1
  readonly property int configuredBlankDelay: {
    var cfg = root.settingsConfig
    var list = cfg && Array.isArray(cfg.plugins) ? cfg.plugins : []
    for (var i = 0; i < list.length; i++) {
      var entry = list[i]
      if (entry && String(entry.id || "") === pluginId && entry.blankMs !== undefined)
        return Math.max(1000, Math.min(3600000, Number(entry.blankMs) || defaultBlankDelay))
    }
    return defaultBlankDelay
  }
  readonly property int blankDelay: blankDelayOverride >= 0 ? blankDelayOverride : configuredBlankDelay

  // When true the display stays powered while locked: the DPMS-off is skipped
  // entirely, so video designs keep playing and slow monitors are never
  // re-blanked. Takes precedence over blankDelay. Lives on the plugin entry
  // in shell.json.
  property int keepDisplayOnOverride: -1
  readonly property bool configuredKeepDisplayOn: {
    var cfg = root.settingsConfig
    var list = cfg && Array.isArray(cfg.plugins) ? cfg.plugins : []
    for (var i = 0; i < list.length; i++) {
      var entry = list[i]
      if (entry && String(entry.id || "") === pluginId) return entry.keepDisplayOn === true
    }
    return false
  }
  readonly property bool keepDisplayOn: keepDisplayOnOverride >= 0 ? keepDisplayOnOverride === 1 : configuredKeepDisplayOn

  // Some monitors drop off the bus when DPMS turns them off (a lone DisplayPort
  // panel on NVIDIA, issue #34). With no output left while the session lock is
  // held the shell dies, relaunches, recovers the stranded lock and blanks
  // again -- a loop for as long as the session stays locked. The blank leaves
  // a marker in the runtime dir that the wake removes, so a shell that starts
  // up and finds it knows its predecessor died with the display off, and
  // keeps the displays lit for the rest of this login instead.
  readonly property string blankMarkerDir: (Quickshell.env("XDG_RUNTIME_DIR") || "/tmp") + "/omarchy-lock-explorer"
  property bool blankCrashed: false

  // Opt-in HDMI workaround; Never still applies independently on every setup.
  readonly property bool displayBlankingSuppressed: keepDisplayOn || blankCrashed || DisplayPower.keepDisplaysOn(
    root.settingsConfig, pluginId, Quickshell.screens)
  onDisplayBlankingSuppressedChanged: {
    // The one-shot blank timer may already have fired while HDMI was present.
    // Start a fresh delay on disconnect (or when the option is turned off).
    if (lockRequested && !displayBlankingSuppressed) armBlankTimer()
  }

  // Avatar picture for the designs that show the user. The chosen path lives on
  // the plugin entry in shell.json; "none" there means the user cleared it and
  // wants the initial back, an empty setting falls back to the usual dotfiles.
  property string avatarOverride: ""
  readonly property string configuredAvatar: {
    var cfg = root.settingsConfig
    var list = cfg && Array.isArray(cfg.plugins) ? cfg.plugins : []
    for (var i = 0; i < list.length; i++) {
      var entry = list[i]
      if (entry && String(entry.id || "") === pluginId && entry.avatar) return String(entry.avatar)
    }
    return ""
  }
  property string detectedAvatar: ""
  property int avatarVersion: 0
  readonly property string avatarSetting: avatarOverride.length > 0 ? avatarOverride : configuredAvatar
  readonly property string avatarPath: avatarSetting === "none" ? ""
    : (avatarSetting.length > 0 ? avatarSetting : detectedAvatar)
  readonly property string avatarUrl: {
    if (avatarPath.length === 0) return ""
    var encoded = String(avatarPath).split("/").map(encodeURIComponent).join("/")
    return "file://" + encoded + "?v=" + avatarVersion
  }

  // A looping video for the designs that show one (Motion), and a clip that
  // plays over the desktop right after the password checks out. Both live on
  // the plugin entry in shell.json, as `video` and `sting`; "none" there means
  // it was cleared on purpose.
  property string videoOverride: ""
  readonly property string configuredVideo: {
    var cfg = root.settingsConfig
    var list = cfg && Array.isArray(cfg.plugins) ? cfg.plugins : []
    for (var i = 0; i < list.length; i++) {
      var entry = list[i]
      if (entry && String(entry.id || "") === pluginId && entry.video) return String(entry.video)
    }
    return ""
  }
  readonly property string videoSetting: videoOverride.length > 0 ? videoOverride : configuredVideo
  readonly property string videoPath: videoSetting === "none" ? "" : videoSetting

  property string stingOverride: ""
  readonly property string configuredSting: {
    var cfg = root.settingsConfig
    var list = cfg && Array.isArray(cfg.plugins) ? cfg.plugins : []
    for (var i = 0; i < list.length; i++) {
      var entry = list[i]
      if (entry && String(entry.id || "") === pluginId && entry.sting) return String(entry.sting)
    }
    return ""
  }
  readonly property string stingSetting: stingOverride.length > 0 ? stingOverride : configuredSting
  readonly property string stingPath: stingSetting === "none" ? "" : stingSetting
  readonly property string stingUrl: {
    if (stingPath.length === 0) return ""
    var encoded = String(stingPath).split("/").map(encodeURIComponent).join("/")
    return "file://" + encoded
  }

  property int stingVolumeOverride: -1
  readonly property int configuredStingVolume: {
    var cfg = root.settingsConfig
    var list = cfg && Array.isArray(cfg.plugins) ? cfg.plugins : []
    for (var i = 0; i < list.length; i++) {
      var entry = list[i]
      if (entry && String(entry.id || "") === pluginId && entry.stingVolume !== undefined)
        return Math.max(0, Math.min(100, Number(entry.stingVolume) || 0))
    }
    return 0
  }
  readonly property int stingVolume: stingVolumeOverride >= 0 ? stingVolumeOverride : configuredStingVolume

  // How fast unlock clips play, 1.0 is natural speed. Applies to the clip
  // designs and the separate unlock clip. Saved on the plugin entry as
  // `clipSpeed` when it is not 1.
  property real clipSpeedOverride: -1
  readonly property real configuredClipSpeed: {
    var cfg = root.settingsConfig
    var list = cfg && Array.isArray(cfg.plugins) ? cfg.plugins : []
    for (var i = 0; i < list.length; i++) {
      var entry = list[i]
      if (entry && String(entry.id || "") === pluginId && entry.clipSpeed !== undefined) {
        var v = Number(entry.clipSpeed)
        if (isFinite(v) && v > 0) return Math.max(0.25, Math.min(4, v))
      }
    }
    return 1
  }
  readonly property real clipSpeed: clipSpeedOverride > 0 ? clipSpeedOverride : configuredClipSpeed

  function setClipSpeed(v) {
    var speed = Number(v)
    if (!isFinite(speed) || speed <= 0) return false
    speed = Math.max(0.25, Math.min(4, speed))
    clipSpeedOverride = speed
    if (shell && typeof shell.updateEntryInline === "function") {
      var current = pluginEntry()
      if (speed === 1) delete current.clipSpeed
      else current.clipSpeed = speed
      writeEntry(current)
    }
    logEvent("clip-speed=" + speed)
    return true
  }

  // 24-hour clocks by default; on, every design's clock reads 12-hour with
  // AM/PM. Saved on the plugin entry as `clock12` only when it is on, so a
  // 24-hour setup keeps the entry it always had.
  property int twelveHourOverride: -1
  readonly property bool configuredTwelveHour: {
    var cfg = root.settingsConfig
    var list = cfg && Array.isArray(cfg.plugins) ? cfg.plugins : []
    for (var i = 0; i < list.length; i++) {
      var entry = list[i]
      if (entry && String(entry.id || "") === pluginId && entry.clock12 !== undefined)
        return entry.clock12 === true || String(entry.clock12) === "true"
    }
    return false
  }
  readonly property bool twelveHour: twelveHourOverride >= 0 ? twelveHourOverride === 1 : configuredTwelveHour

  function setTwelveHour(v) {
    var on = v === true || v === 1 || v === "true" || v === "12" || v === "on"
    twelveHourOverride = on ? 1 : 0
    if (shell && typeof shell.updateEntryInline === "function") {
      var current = pluginEntry()
      if (on) current.clock12 = true
      else delete current.clock12
      writeEntry(current)
    }
    logEvent("clock=" + (on ? "12h" : "24h"))
    return true
  }

  // Security-key unlock, on whenever a key is set up. Saved on the plugin
  // entry as `fido2Off` only when it is turned off, so a setup that never
  // touches this keeps the entry it always had.
  property int fido2EnabledOverride: -1
  readonly property bool configuredFido2Enabled: {
    var cfg = root.settingsConfig
    var list = cfg && Array.isArray(cfg.plugins) ? cfg.plugins : []
    for (var i = 0; i < list.length; i++) {
      var entry = list[i]
      if (entry && String(entry.id || "") === pluginId && entry.fido2Off !== undefined)
        return !(entry.fido2Off === true || String(entry.fido2Off) === "true")
    }
    return true
  }
  readonly property bool fido2Enabled: fido2EnabledOverride >= 0 ? fido2EnabledOverride === 1 : configuredFido2Enabled

  function setFido2Enabled(v) {
    var on = v === true || v === 1 || v === "true" || v === "on"
    fido2EnabledOverride = on ? 1 : 0
    if (shell && typeof shell.updateEntryInline === "function") {
      var current = pluginEntry()
      if (on) delete current.fido2Off
      else current.fido2Off = true
      writeEntry(current)
    }
    // Turning it off mid-lock hands the screen back to the password.
    if (!on && fido2Active) setAuthMode("password")
    logEvent("fido2=" + (on ? "on" : "off"))
    return true
  }

  readonly property string backgroundUrl: {
    if (backgroundPath.length === 0) return ""
    var encoded = String(backgroundPath).split("/").map(encodeURIComponent).join("/")
    return "file://" + encoded + "?v=" + backgroundVersion
  }

  function showsInput(screen) {
    if (inputMonitor === "all" || !screen) return true
    var names = []
    var screens = Quickshell.screens || []
    for (var i = 0; i < screens.length; i++) if (screens[i] && screens[i].name) names.push(screens[i].name)
    if (names.indexOf(inputMonitor) === -1) return true
    return screen.name === inputMonitor
  }

  function pluginEntry() {
    var cfg = root.settingsConfig
    var list = cfg && Array.isArray(cfg.plugins) ? cfg.plugins : []
    for (var i = 0; i < list.length; i++)
      if (list[i] && String(list[i].id || "") === pluginId) return Util.cloneJson(list[i])
    return {}
  }

  // Every setting is saved read-modify-write on this plugin's entry, and the
  // host replaces the entry with what it is handed. The write-through copy in
  // LocalSettings is what keeps two changes made back to back from clobbering
  // each other while the file watcher catches up.
  function writeEntry(entry) {
    localSettings.remember(entry)
    return shell.updateEntryInline(pluginId, entry)
  }

  function setInputMonitor(name) {
    var value = String(name || "all")
    inputMonitorOverride = value
    if (shell && typeof shell.updateEntryInline === "function") {
      var current = pluginEntry()
      if (value === "all") delete current.inputMonitor
      else current.inputMonitor = value
      writeEntry(current)
    }
    logEvent("input-monitor=" + value)
    return true
  }

  function setUnlockAnimation(name) {
    var value = String(name || "").trim().toLowerCase()
    if (unlockAnimations.indexOf(value) === -1) return false

    unlockOverride = value
    if (shell && typeof shell.updateEntryInline === "function") {
      var current = pluginEntry()
      if (value === "none") delete current.unlock
      else current.unlock = value
      writeEntry(current)
    }
    logEvent("unlock=" + value)
    return true
  }

  function setUnlockDuration(ms) {
    var text = String(ms === undefined ? "" : ms).trim()
    var value = Math.round(Number(text))
    if (text.length === 0 || !isFinite(value) || value < 0 || value > 2000) return false

    unlockDurationOverride = value
    if (shell && typeof shell.updateEntryInline === "function") {
      var current = pluginEntry()
      if (value === defaultUnlockDuration) delete current.unlockMs
      else current.unlockMs = value
      writeEntry(current)
    }
    logEvent("unlock-ms=" + value)
    return true
  }

  function setBlankDelay(ms) {
    var text = String(ms === undefined ? "" : ms).trim()
    var value = Math.round(Number(text))
    if (text.length === 0 || !isFinite(value) || value < 1000 || value > 3600000) return false

    blankDelayOverride = value
    if (shell && typeof shell.updateEntryInline === "function") {
      var current = pluginEntry()
      if (value === defaultBlankDelay) delete current.blankMs
      else current.blankMs = value
      writeEntry(current)
    }
    logEvent("blank-ms=" + value)
    return true
  }

  function setKeepDisplayOn(on) {
    var value = on === true || on === 1 || on === "true"
    keepDisplayOnOverride = value ? 1 : 0
    if (shell && typeof shell.updateEntryInline === "function") {
      var current = pluginEntry()
      if (!value) delete current.keepDisplayOn
      else current.keepDisplayOn = true
      writeEntry(current)
    }
    logEvent("keep-display-on=" + value)
    return true
  }

  property string previewDesignId: ""
  property string previewTyped: ""
  property string previewFailure: ""
  property bool previewUnlocking: false
  Timer { id: previewFailureTimer; interval: 2500; onTriggered: root.previewFailure = "" }

  // Runs the unlock animation on the preview so it can be seen without locking.
  Timer {
    id: previewUnlockTimer
    interval: Math.max(1, root.unlockDuration + 80)
    repeat: false
    onTriggered: {
      root.previewVisible = false
      root.previewDesignId = ""
      root.previewTyped = ""
      root.previewUnlocking = false
    }
  }

  readonly property string userDesignsDir: home + "/.config/omarchy/lock-designs"
  property int designsRevision: 0

  function rescanUserDesigns() {
    if (!userDesignsProc.running) userDesignsProc.running = true
  }

  // The clip designs' videos ship in <plugin>/videos; link any that are
  // missing into ~/.config/omarchy/lock-videos, where ClipDesign and the
  // boot twins resolve them. Idempotent, never overwrites a user's file.
  Process {
    id: shippedClipsProc
    running: true
    command: ["bash", "-c",
      "dst=\"$HOME/.config/omarchy/lock-videos\"; mkdir -p \"$dst\"; for f in \"$0\"/videos/*.mp4; do [ -e \"$f\" ] || continue; b=$(basename \"$f\"); [ -e \"$dst/$b\" ] || ln -s \"$f\" \"$dst/$b\"; done",
      root.pluginDir]
  }

  // Bumps the revision so every LockHost showing a user design reloads it.
  function reloadDesigns() {
    designsRevision += 1
  }

  readonly property string pluginDir: {
    var u = String(Qt.resolvedUrl("."))
    return decodeURIComponent(u.replace(/^file:\/\//, "")).replace(/\/$/, "")
  }

  readonly property string checkFaceAuthPath: pluginDir + "/check-face-auth.sh"
  readonly property string checkFido2AuthPath: pluginDir + "/check-fido2-auth.sh"

  // The app launcher entry and the Omarchy menu entry (Style -> Lock Screen).
  // A plugin cannot run anything when it is installed, so they are opt-in:
  // the Settings tab's "Omarchy menu" row and `omarchy-shell lock setMenuEntry`
  // run extras/install.sh, which also takes them out again.
  readonly property string menuInstallPath: pluginDir + "/extras/install.sh"

  // Every embedded script that writes a file of the user's sources this first
  // (it arrives as the script's $0): an owner-checked, symlink-free directory
  // chain under $HOME and atomic replacement from inside the target directory.
  readonly property string safePathsLib: pluginDir + "/extras/safe-paths.sh"
  property bool menuEntryInstalled: false

  function refreshMenuEntry() {
    if (!menuEntryStatusProc.running) menuEntryStatusProc.running = true
  }

  function setMenuEntry(v) {
    var on = v === true || v === 1 || v === "true" || v === "on" || v === "1"
    if (menuEntryApplyProc.running) return false
    menuEntryApplyProc.command = on ? [menuInstallPath] : [menuInstallPath, "--remove"]
    menuEntryApplyProc.running = true
    logEvent("menu-entry=" + (on ? "on" : "off"))
    return true
  }

  Process {
    id: menuEntryStatusProc
    command: [root.menuInstallPath, "--status"]
    stdout: StdioCollector { id: menuEntryStatusOut; waitForEnd: true }
    onExited: root.menuEntryInstalled = String(menuEntryStatusOut.text || "").trim() === "installed"
  }

  Process {
    id: menuEntryApplyProc
    onExited: root.refreshMenuEntry()
  }

  signal designCustomized(string id, string path)

  function customizeDesign(id) {
    var d = String(id || "") === "new"
      ? { id: "new", file: "MyDesign.qml", name: "new", template: true }
      : Designs.byId(String(id || ""))
    if (!d) return false
    if (d.path) { designCustomized(d.id, decodeURIComponent(d.path.replace(/^file:\/\//, ""))); return true }
    var source = d.template ? pluginDir + "/extras/lock-designs/" + d.file : pluginDir + "/designs/" + d.file
    if (customizeProc.running) return false
    var importLine = 'import "../plugins/' + pluginId + '/designs"'
    customizeProc.command = ["bash", "-c", customizeScript, safePathsLib, source, userDesignsDir, d.file.replace(/\.qml$/, ""), d.template ? "" : importLine, d.name]
    customizeProc.running = true
    return true
  }

  readonly property string customizeScript: '
set -e; source "$0"
src="$1"; dir="$2"; base="$3"; imp="$4"; name="$5"
safe_dir "$dir"
target="$dir/$base.qml"; n=2
while [[ -e "$target" ]]; do target="$dir/$base$n.qml"; n=$((n+1)); done
{
  if [[ $name == new ]]; then echo "// New design. Edit it in the explorer (E) or with:"; else echo "// Customized copy of the $name design. Edit it here or with:"; fi
  echo "//   omarchy-shell lock editDesign my-$(basename "$target" .qml | tr "[:upper:]" "[:lower:]")"
  awk -v imp="$imp" \'
    /^import / { last = NR }
    { lines[NR] = $0 }
    END { for (i = 1; i <= NR; i++) { print lines[i]; if (i == last && imp != "") print imp } }
  \' "$src"
} | put_file "$dir" "$(basename "$target")"
echo "$target"
'

  Process {
    id: customizeProc
    stdout: StdioCollector {
      id: customizeOut
      waitForEnd: true
      onStreamFinished: {
        var target = String(customizeOut.text || "").trim()
        if (target.length === 0) return
        var d = Designs.fromUserFile(target)
        Designs.setUser(Designs.USER.concat([d]))
        root.designsRevision += 1
        root.designCustomized(d.id, target)
        root.rescanUserDesigns()
      }
    }
  }

  // ---------------------------------------------------------------- designer
  //
  // The visual designer writes ordinary designs into the same folder; a
  // layout comment at the top is what tells it apart, and is what lets the
  // designer open one again. Explorer.qml owns the editing, the service owns
  // the files.

  signal designerDesignCreated(string id, string path)

  // Makes the file and hands its path back, so the explorer can drop straight
  // into the designer on it.
  function createDesignerDesign(content) {
    if (designerCreateProc.running) return false
    designerCreateProc.command = ["bash", "-c", designerCreateScript, safePathsLib, userDesignsDir, String(content)]
    designerCreateProc.running = true
    return true
  }

  readonly property string designerCreateScript: '
set -e; source "$0"
dir="$1"; content="$2"
safe_dir "$dir"
target="$dir/MyLayout.qml"; n=2
while [[ -e $target ]]; do target="$dir/MyLayout$n.qml"; n=$((n+1)); done
printf %s "$content" | put_file "$dir" "$(basename "$target")"
echo "$target"
'

  Process {
    id: designerCreateProc
    stdout: StdioCollector {
      id: designerCreateOut
      waitForEnd: true
      onStreamFinished: {
        var target = String(designerCreateOut.text || "").trim()
        if (target.length === 0) return
        var d = Designs.fromUserFile(target)
        d.designer = true
        Designs.setUser(Designs.USER.concat([d]))
        root.designsRevision += 1
        root.designerDesignCreated(d.id, target)
        root.rescanUserDesigns()
        root.logEvent("designer-design=" + d.id)
      }
    }
  }

  // -------------------------------------------------------------- components
  //
  // Pieces saved out of the designer, so they can be dropped into any design
  // later. One JSON file each in ~/.config/omarchy/lock-components, written
  // on a single line; the scan below reads them all in one go.

  readonly property string componentsDir: home + "/.config/omarchy/lock-components"
  property var components: []

  function rescanComponents() {
    if (!componentsProc.running) componentsProc.running = true
  }

  function saveComponent(slug, json) {
    var safe = String(slug || "").toLowerCase().replace(/[^a-z0-9-]+/g, "-").replace(/^-+|-+$/g, "")
    if (safe.length === 0 || String(json || "").length === 0) return false
    if (componentSaveProc.running) return false
    componentSaveProc.command = ["bash", "-c",
      'mkdir -p "$1" && printf %s "$3" > "$1/$2.json"', "savecomponent", componentsDir, safe, String(json)]
    componentSaveProc.running = true
    logEvent("component-saved=" + safe)
    return true
  }

  function deleteComponent(slug) {
    var safe = String(slug || "").toLowerCase().replace(/[^a-z0-9-]+/g, "-").replace(/^-+|-+$/g, "")
    if (safe.length === 0) return false
    if (componentDeleteProc.running) return false
    componentDeleteProc.command = ["bash", "-c",
      'rm -f -- "$1/$2.json"', "delcomponent", componentsDir, safe]
    componentDeleteProc.running = true
    logEvent("component-deleted=" + safe)
    return true
  }

  Process {
    id: componentsProc
    running: true
    // slug<TAB>json, one component per line. The files the designer writes
    // have no newlines in them; tr keeps a hand-edited one from splitting the
    // listing across lines.
    command: ["bash", "-c",
      'dir="$1"; mkdir -p "$dir"; for f in "$dir"/*.json; do [ -e "$f" ] || continue; printf "%s\\t" "$(basename "$f" .json)"; tr -d "\\n" < "$f"; printf "\\n"; done',
      "components", root.componentsDir]
    stdout: StdioCollector {
      id: componentsOut
      waitForEnd: true
      onStreamFinished: {
        var lines = String(componentsOut.text || "").split("\n")
        var list = []
        for (var i = 0; i < lines.length; i++) {
          var tab = lines[i].indexOf("\t")
          if (tab === -1) continue
          var slug = lines[i].substring(0, tab).trim()
          try {
            var comp = JSON.parse(lines[i].substring(tab + 1))
            if (comp && comp.nodes instanceof Array) list.push({ slug: slug, comp: comp })
          } catch (e) {
            console.warn("lock-explorer: cannot read component", slug, e)
          }
        }
        list.sort(function(a, b) { return String(a.comp.name).localeCompare(String(b.comp.name)) })
        if (JSON.stringify(list) === JSON.stringify(root.components)) return
        root.components = list
      }
    }
  }

  Process {
    id: componentSaveProc
    onExited: root.rescanComponents()
  }

  Process {
    id: componentDeleteProc
    onExited: root.rescanComponents()
  }

  // The file dialog again, for the designer's Image piece. Same dance as the
  // avatar: the explorer steps aside while it is up.
  signal imagePicked(string path)
  property bool imagePickReopens: false

  function pickImage(reopenExplorer) {
    if (imagePickProc.running) return false
    imagePickReopens = reopenExplorer === true
    imagePickProc.running = true
    return true
  }

  Process {
    id: imagePickProc
    command: ["omarchy-file-select", "--title", "Pick an image for the design", "--extensions", "png jpg jpeg webp svg"]
    stdout: StdioCollector {
      id: imagePickOut
      waitForEnd: true
      onStreamFinished: {
        var picked = String(imagePickOut.text || "").trim().split("\n")[0] || ""
        if (picked.length > 0) root.imagePicked(picked)
        if (root.imagePickReopens && root.shell && typeof root.shell.summon === "function")
          root.shell.summon(root.pluginId, "{}")
        root.imagePickReopens = false
      }
    }
  }

  // ------------------------------------------------------------------ avatar

  function setAvatar(path) {
    var value = String(path || "").trim()
    if (value.indexOf("file://") === 0) value = decodeURIComponent(value.replace(/^file:\/\//, ""))
    var setting = value.length > 0 ? value : "none"
    avatarOverride = setting
    if (shell && typeof shell.updateEntryInline === "function") {
      var current = pluginEntry()
      current.avatar = setting
      writeEntry(current)
    }
    avatarVersion += 1
    logEvent("avatar=" + setting)
    return true
  }

  function clearAvatar() {
    return setAvatar("")
  }

  // Back to the detected ~/.face and friends.
  function resetAvatar() {
    avatarOverride = ""
    if (shell && typeof shell.updateEntryInline === "function") {
      var current = pluginEntry()
      delete current.avatar
      writeEntry(current)
    }
    avatarVersion += 1
    detectAvatar()
    logEvent("avatar=auto")
    return true
  }

  function detectAvatar() {
    if (!detectAvatarProc.running) detectAvatarProc.running = true
  }

  // The desktop file chooser, so picking a picture is a normal file dialog.
  // The explorer grabs the keyboard, so it closes itself before asking for one
  // and comes back when the dialog is answered.
  property bool avatarPickReopens: false
  function pickAvatar(reopenExplorer) {
    if (avatarPickProc.running) return false
    avatarPickReopens = reopenExplorer === true
    avatarPickProc.running = true
    return true
  }

  readonly property string detectAvatarScript: '
for f in "$HOME/.config/omarchy/lock-avatar.png" "$HOME/.config/omarchy/lock-avatar.jpg" \
         "$HOME/.config/omarchy/lock-avatar.jpeg" "$HOME/.config/omarchy/lock-avatar.webp" \
         "$HOME/.face" "$HOME/.face.icon" "/var/lib/AccountsService/icons/$USER"; do
  [[ -f $f ]] && { echo "$f"; exit 0; }
done
'

  Process {
    id: detectAvatarProc
    command: ["bash", "-c", root.detectAvatarScript]
    stdout: StdioCollector {
      id: detectAvatarOut
      waitForEnd: true
      onStreamFinished: {
        var found = String(detectAvatarOut.text || "").trim().split("\n")[0] || ""
        if (found !== root.detectedAvatar) {
          root.detectedAvatar = found
          root.avatarVersion += 1
        }
      }
    }
  }

  Process {
    id: avatarPickProc
    command: ["omarchy-file-select", "--title", "Pick a lock screen avatar", "--extensions", "png jpg jpeg webp"]
    stdout: StdioCollector {
      id: avatarPickOut
      waitForEnd: true
      onStreamFinished: {
        var picked = String(avatarPickOut.text || "").trim().split("\n")[0] || ""
        if (picked.length > 0) root.setAvatar(picked)
        if (root.avatarPickReopens && root.shell && typeof root.shell.summon === "function")
          root.shell.summon(root.pluginId, "{}")
        root.avatarPickReopens = false
      }
    }
  }

  // ------------------------------------------------------------------- video

  function setVideo(path) {
    var value = String(path || "").trim()
    if (value.indexOf("file://") === 0) value = decodeURIComponent(value.replace(/^file:\/\//, ""))
    var setting = value.length > 0 ? value : "none"
    videoOverride = setting
    if (shell && typeof shell.updateEntryInline === "function") {
      var current = pluginEntry()
      if (setting === "none") delete current.video
      else current.video = setting
      writeEntry(current)
    }
    logEvent("video=" + setting)
    return true
  }

  function clearVideo() { return setVideo("") }

  function setSting(path) {
    var value = String(path || "").trim()
    if (value.indexOf("file://") === 0) value = decodeURIComponent(value.replace(/^file:\/\//, ""))
    var setting = value.length > 0 ? value : "none"
    stingOverride = setting
    if (shell && typeof shell.updateEntryInline === "function") {
      var current = pluginEntry()
      if (setting === "none") delete current.sting
      else current.sting = setting
      writeEntry(current)
    }
    logEvent("sting=" + setting)
    return true
  }

  function clearSting() { return setSting("") }

  function setStingVolume(value) {
    var text = String(value === undefined ? "" : value).trim()
    var v = Math.round(Number(text))
    if (text.length === 0 || !isFinite(v) || v < 0 || v > 100) return false
    stingVolumeOverride = v
    if (shell && typeof shell.updateEntryInline === "function") {
      var current = pluginEntry()
      if (v === 0) delete current.stingVolume
      else current.stingVolume = v
      writeEntry(current)
    }
    logEvent("sting-volume=" + v)
    return true
  }

  // Same dance as the avatar picker: the explorer steps aside for the dialog.
  property bool videoPickReopens: false
  property string videoPickTarget: "video"
  function pickVideo(reopenExplorer, target) {
    if (videoPickProc.running) return false
    videoPickTarget = String(target || "video")
    videoPickReopens = reopenExplorer === true
    videoPickProc.command = ["omarchy-file-select",
      "--title", videoPickTarget === "sting" ? "Pick an unlock clip"
        : videoPickTarget === "clip" ? "Pick a video for a new clip design"
        : "Pick a lock screen video",
      "--extensions", "mp4 mkv webm mov m4v"]
    videoPickProc.running = true
    return true
  }

  Process {
    id: videoPickProc
    stdout: StdioCollector {
      id: videoPickOut
      waitForEnd: true
      onStreamFinished: {
        var picked = String(videoPickOut.text || "").trim().split("\n")[0] || ""
        if (picked.length > 0) {
          if (root.videoPickTarget === "sting") root.setSting(picked)
          else if (root.videoPickTarget === "clip") root.createClipDesign(picked)
          else root.setVideo(picked)
        }
        if (root.videoPickReopens && root.shell && typeof root.shell.summon === "function")
          root.shell.summon(root.pluginId, "{}")
        root.videoPickReopens = false
      }
    }
  }

  // A picked video becomes a one-line ClipDesign in ~/.config/omarchy/lock-designs,
  // with the file itself copied to ~/.config/omarchy/lock-videos where ClipDesign
  // resolves clips from. Same freeze-then-play-on-unlock behavior as Storm.
  signal clipDesignAdded(string id)

  function createClipDesign(path) {
    var src = String(path || "").trim()
    if (src.indexOf("file://") === 0) src = decodeURIComponent(src.replace(/^file:\/\//, ""))
    if (src.length === 0 || clipDesignProc.running) return false
    var importLine = 'import "../plugins/' + pluginId + '/designs"'
    clipDesignProc.command = ["bash", "-c", clipDesignScript, safePathsLib,
      src, home + "/.config/omarchy/lock-videos", userDesignsDir, importLine]
    clipDesignProc.running = true
    return true
  }

  readonly property string clipDesignScript: '
set -e; source "$0"
src="$1"; videos="$2"; dir="$3"; imp="$4"
safe_dir "$videos"; safe_dir "$dir"
base=$(basename "$src")
# The copied name is embedded in QML, comments and tab-separated metadata.
# Keep it inert in all three, and compatible with the clipName scanner.
# Only the imported copy is renamed; the source file is left untouched.
base=$(printf %s "$base" | LC_ALL=C tr -c "a-zA-Z0-9._-" "_")
if [[ -e "$videos/$base" ]] && ! cmp -s "$src" "$videos/$base"; then
  stem="${base%.*}"; ext="${base##*.}"; n=2
  while [[ -e "$videos/$stem-$n.$ext" ]]; do n=$((n+1)); done
  base="$stem-$n.$ext"
fi
[[ -e "$videos/$base" ]] || put_file "$videos" "$base" < "$src"
stem="${base%.*}"
name=$(printf %s "$stem" | tr -cd "[:alnum:]_-")
[[ -n "$name" ]] || name=Clip
target="$dir/$name.qml"; n=2
while [[ -e "$target" ]]; do target="$dir/$name$n.qml"; n=$((n+1)); done
q=\'"\'
{
  echo "// Clip design: $base holds its first frame while locked and plays"
  echo "// through as the unlock. The video lives in ~/.config/omarchy/lock-videos."
  echo "$imp"
  echo ""
  echo "ClipDesign { clipName: $q$base$q }"
} | put_file "$dir" "$(basename "$target")"
printf "%s\\t%s\\n" "$target" "$base"
'

  Process {
    id: clipDesignProc
    stdout: StdioCollector {
      id: clipDesignOut
      waitForEnd: true
      onStreamFinished: {
        var last = String(clipDesignOut.text || "").trim().split("\n").pop() || ""
        var parts = last.split("\t")
        var target = (parts[0] || "").trim()
        if (target.length === 0) return
        var d = Designs.fromUserFile(target)
        d.anim = true
        d.clip = true
        if (parts.length > 1 && parts[1].trim().length > 0) d.clipFile = parts[1].trim()
        Designs.setUser(Designs.USER.concat([d]))
        root.designsRevision += 1
        root.rescanUserDesigns()
        root.clipDesignAdded(d.id)
        root.logEvent("clip-design=" + target)
      }
    }
  }

  // Delete a user design from ~/.config/omarchy/lock-designs. A clip design's
  // video goes with it, unless the boot screen, the Motion video or the unlock
  // clip still point at it, or another design names it.
  function deleteDesign(id) {
    var d = Designs.byId(String(id || ""))
    if (!d || !d.path) return false
    var file = decodeURIComponent(String(d.path).replace(/^file:\/\//, ""))
    if (file.indexOf(userDesignsDir + "/") !== 0) return false
    var clip = String(d.clipFile || "")
    if (clip.length > 0) {
      var tail = "/" + clip
      var keep = bootSetting === "video:" + clip
        || (videoPath.length >= tail.length && videoPath.lastIndexOf(tail) === videoPath.length - tail.length)
        || (stingPath.length >= tail.length && stingPath.lastIndexOf(tail) === stingPath.length - tail.length)
      if (keep) clip = ""
    }
    if (designId === d.id) setDesign(Designs.DEFAULT_ID)
    deleteDesignProc.command = ["bash", "-c", deleteDesignScript, "deldesign",
      file, home + "/.config/omarchy/lock-videos", clip, userDesignsDir]
    deleteDesignProc.running = true
    logEvent("delete-design=" + d.id)
    return true
  }

  readonly property string deleteDesignScript: '
set -e
file="$1"; videos="$2"; clip="$3"; dir="$4"
rm -f -- "$file"
if [[ -n $clip ]] && ! grep -qs -- "$clip" "$dir"/*.qml 2>/dev/null; then
  rm -f -- "$videos/$clip"
fi
'

  Process {
    id: deleteDesignProc
    onExited: function(exitCode) {
      root.rescanUserDesigns()
      root.refreshBootLists()
    }
  }

  // Delete a boot card of the user's own: a video from
  // ~/.config/omarchy/lock-videos (video:<file>) or a custom boot layout
  // (custom:<name>, taking its generated lock design along). Videos still
  // used as the Motion video or the unlock clip are left alone.
  function deleteBootItem(id) {
    var v = String(id || "")
    if (v.indexOf("video:") !== 0 && v.indexOf("custom:") !== 0) return false
    if (v.indexOf("video:") === 0) {
      var tail = "/" + v.substring(6)
      if ((videoPath.length >= tail.length && videoPath.lastIndexOf(tail) === videoPath.length - tail.length)
        || (stingPath.length >= tail.length && stingPath.lastIndexOf(tail) === stingPath.length - tail.length)) return false
    }
    if (bootSetting === v) setBoot("stock")
    deleteBootItemProc.command = ["bash", "-c", deleteBootItemScript, "delboot",
      v, home + "/.config/omarchy/lock-videos", home + "/.config/omarchy/boot-designs",
      home + "/.config/omarchy/lock-designs",
      home + "/.local/state/omarchy/lock-explorer-boot-previews"]
    deleteBootItemProc.running = true
    logEvent("delete-boot=" + v)
    return true
  }

  readonly property string deleteBootItemScript: '
set -e
id="$1"; videos="$2"; bootdir="$3"; lockdir="$4"; previews="$5"
case "$id" in
  video:*)
    f="${id#video:}"
    rm -f -- "$videos/$f"
    rm -f -- "$previews/video-$f-"*.png
    ;;
  custom:*)
    n="${id#custom:}"
    rm -f -- "$bootdir/$n.conf"
    rm -f -- "$lockdir/$n.qml"
    rm -f -- "$previews/custom-$n-"*.png
    ;;
esac
'

  Process {
    id: deleteBootItemProc
    onExited: function(exitCode) {
      root.refreshBootLists()
      root.rescanUserDesigns()
      root.refreshBootPreviews()
    }
  }

  // The clip that plays once the lock surface is gone. It runs over the live
  // desktop rather than holding the session lock, so nothing it does can leave
  // the screen stuck: any key, any click, the end of the clip or the failsafe
  // timer takes it away.
  property bool stingPlaying: false

  function playSting() {
    if (stingPath.length === 0 || stingPlaying) return false
    // No player without qt6-multimedia; setting stingPlaying anyway would
    // stick, since only the window's timers ever call endSting().
    if (!multimediaAvailable) return false
    stingPlaying = true
    logEvent("sting-playing")
    return true
  }

  function endSting() {
    if (!stingPlaying) return
    stingPlaying = false
    logEvent("sting-done")
    commitClipWallpaper()
  }

  // "The last frame becomes your wallpaper": with clipWallpaper on, the frame
  // the unlock video ends on is extracted while the screen is still locked and
  // handed to omarchy-theme-bg-set the moment the clip gives the screen back,
  // so the desktop opens exactly where the video stopped. Saved on the plugin
  // entry as `clipWallpaper: true`, off by default.
  property int clipWallpaperOverride: -1
  readonly property bool configuredClipWallpaper: {
    var cfg = root.settingsConfig
    var list = cfg && Array.isArray(cfg.plugins) ? cfg.plugins : []
    for (var i = 0; i < list.length; i++) {
      var entry = list[i]
      if (entry && String(entry.id || "") === pluginId && entry.clipWallpaper === true) return true
    }
    return false
  }
  readonly property bool clipWallpaper: clipWallpaperOverride === -1 ? configuredClipWallpaper : clipWallpaperOverride === 1

  function setClipWallpaper(on) {
    var enabled = on === true || on === "true" || on === "on"
    clipWallpaperOverride = enabled ? 1 : 0
    if (shell && typeof shell.updateEntryInline === "function") {
      var current = pluginEntry()
      if (enabled) current.clipWallpaper = true
      else delete current.clipWallpaper
      writeEntry(current)
    }
    logEvent("clip-wallpaper=" + enabled)
    return true
  }

  property string preparedClipWallpaper: ""

  function prepareClipWallpaper(path) {
    if (!clipWallpaper || !path || String(path).length === 0) return
    if (clipWallPrepProc.running) return
    preparedClipWallpaper = ""
    clipWallPrepProc.command = ["bash", "-c", clipWallScript, safePathsLib,
      String(path), home + "/.local/state/omarchy/lock-explorer-clip-wallpapers"]
    clipWallPrepProc.running = true
  }

  function commitClipWallpaper() {
    if (!clipWallpaper || preparedClipWallpaper.length === 0) return
    Quickshell.execDetached(["omarchy-theme-bg-set", preparedClipWallpaper])
    logEvent("clip-wallpaper-set=" + preparedClipWallpaper)
  }

  readonly property string clipWallScript: '
set -e; source "$0"
src="$1"; dir="$2"
safe_dir "$dir"
stem=$(basename "$src"); stem="${stem%.*}"
out="$dir/$stem.png"
if [[ ! -s "$out" || "$src" -nt "$out" ]]; then
  tmp=$(mktemp --suffix=.png)
  trap "rm -f -- $tmp" EXIT
  ffmpeg -y -loglevel error -sseof -1 -i "$src" -update 1 "$tmp" || true
  [[ -s "$tmp" ]] || ffmpeg -y -loglevel error -i "$src" -update 1 "$tmp"
  put_file "$dir" "$stem.png" < "$tmp"
fi
echo "$out"
'

  Process {
    id: clipWallPrepProc
    stdout: StdioCollector {
      id: clipWallPrepOut
      waitForEnd: true
      onStreamFinished: {
        root.preparedClipWallpaper = String(clipWallPrepOut.text || "").trim().split("\n").pop() || ""
      }
    }
  }

  // The boot (LUKS decrypt) screen, styled with Plymouth. Saved on the plugin
  // entry as `boot`: "follow" keeps it matched to the lock design whenever the
  // design has a twin under plymouth/, a design id pins it to that design, and
  // absent means the stock Omarchy boot theme is left alone. Applying bakes the
  // current theme colors into a generated theme and rebuilds the initramfs
  // through a single polkit prompt (see plymouth/apply.sh).
  property string bootOverride: ""
  readonly property string configuredBoot: {
    var cfg = root.settingsConfig
    var list = cfg && Array.isArray(cfg.plugins) ? cfg.plugins : []
    for (var i = 0; i < list.length; i++) {
      var entry = list[i]
      if (entry && String(entry.id || "") === pluginId && entry.boot) return String(entry.boot)
    }
    return "stock"
  }
  readonly property string bootSetting: {
    var value = bootOverride.length > 0 ? bootOverride : configuredBoot
    if (value === "follow" || value === "stock" || value === "theme" || value === "rotate") return value
    if (value.indexOf("snapshot:") === 0 || value.indexOf("video:") === 0 || value.indexOf("custom:") === 0) return value
    var d = Designs.byId(value)
    return d && d.boot === true ? value : "stock"
  }
  property bool bootApplying: false
  property string bootApplyTarget: ""
  property string bootApplied: ""  // last installed twin, "" = stock/untouched
  property int bootAppliedVersion: 0
  readonly property string bootTarget: {
    if (bootSetting === "stock") return "stock"
    if (bootSetting === "theme") return "theme"
    if (bootSetting === "rotate") return ""
    if (bootSetting.indexOf("snapshot:") === 0) return bootSetting
    if (bootSetting === "follow") {
      var d = Designs.byId(designId)
      return d && d.boot === true ? d.id : ""
    }
    return bootSetting
  }

  function setBoot(value) {
    var v = String(value || "").trim().toLowerCase()
    if (v !== "follow" && v !== "stock" && v !== "theme" && v !== "rotate" && v.indexOf("video:") !== 0 && v.indexOf("custom:") !== 0 && v.indexOf("snapshot:") !== 0) {
      var d = Designs.byId(v)
      if (!d || d.boot !== true) return false
    }
    if (bootApplying) return false
    bootOverride = v
    if (shell && typeof shell.updateEntryInline === "function") {
      var current = pluginEntry()
      if (v === "stock") delete current.boot
      else current.boot = v
      writeEntry(current)
    }
    logEvent("boot=" + v)
    // Redesign: picking a boot option only sets the desired choice; the
    // rebuild happens when the Apply button is pressed.
    return true
  }

  // force reapplies even when the target already matches, so a changed Omarchy
  // theme gets its colors baked in again.
  function applyBoot(force, explicitTarget) {
    if (bootApplying) return
    var target = explicitTarget !== undefined && String(explicitTarget).length > 0 ? String(explicitTarget) : bootTarget
    if (target.length === 0) return  // follow, but this design has no twin
    if (target === "stock" && bootApplied.length === 0) return  // nothing to restore
    if (!force && target === bootApplied) return
    bootApplying = true
    bootApplyTarget = target
    bootApplyProc.command = ["env", "BOOT_CLIP_SECONDS=" + bootClipSeconds, "bash", pluginDir + "/plymouth/apply.sh", target]
    bootApplyProc.running = true
  }

  Process {
    id: bootApplyProc
    stdout: StdioCollector { }
    stderr: StdioCollector {
      id: bootApplyErr
      waitForEnd: true
    }
    onExited: function(exitCode) {
      root.bootApplying = false
      if (exitCode === 0) {
        root.bootApplied = root.bootApplyTarget === "stock" ? "" : root.bootApplyTarget
        root.logEvent("boot-applied=" + (root.bootApplied.length > 0 ? root.bootApplied : "stock"))
      } else {
        root.logEvent("boot-apply-failed=" + exitCode)
        console.warn("lock-explorer: plymouth apply failed:", String(bootApplyErr.text || "").trim())
      }
    }
  }

  // apply.sh records what it installed and which Omarchy theme the colors
  // came from; picking it up here keeps the state right across shell restarts
  // and command line applies.
  FileView {
    path: root.stateHome + "/omarchy/lock-explorer-boot"
    watchChanges: true
    printErrors: false
    onLoaded: {
      var parts = String(text()).trim().split(/\s+/)
      var value = parts[0] || ""
      root.bootApplied = value === "stock" ? "" : value
      root.bootAppliedTheme = parts.length > 1 ? parts[1] : ""
      // Cache-buster for the applied-boot preview image, which is rewritten
      // on every apply (same-id re-applies included).
      root.bootAppliedVersion += 1
    }
    onLoadFailed: { root.bootApplied = ""; root.bootAppliedTheme = "" }
    onFileChanged: reload()
  }

  // The boot screen colors are baked in at apply time, so a theme switch
  // would leave it in the old palette right until the retained last frame
  // hands over to the new wallpaper. Regenerate when the theme changes,
  // unless the user opted out (`bootResync: false` on the plugin entry).
  property string bootAppliedTheme: ""
  property string bootCurrentTheme: ""
  property int bootResyncOverride: -1
  readonly property bool configuredBootResync: {
    var cfg = root.settingsConfig
    var list = cfg && Array.isArray(cfg.plugins) ? cfg.plugins : []
    for (var i = 0; i < list.length; i++) {
      var entry = list[i]
      if (entry && String(entry.id || "") === pluginId && entry.bootResync === false) return false
    }
    return true
  }
  readonly property bool bootResync: bootResyncOverride === -1 ? configuredBootResync : bootResyncOverride === 1

  function setBootResync(on) {
    var enabled = on === true || on === "on" || on === "true"
    bootResyncOverride = enabled ? 1 : 0
    if (shell && typeof shell.updateEntryInline === "function") {
      var current = pluginEntry()
      if (enabled) delete current.bootResync
      else current.bootResync = false
      writeEntry(current)
    }
    logEvent("boot-resync=" + (enabled ? "on" : "off"))
    if (enabled) maybeResyncBoot()
    return true
  }

  // Snapshot boot screens can't be re-baked from the old picture: the theme
  // change moved the wallpaper and colors, so the explorer has to take a
  // fresh snapshot (which also carries the entry geometry).
  signal bootResnapshotRequested(string designId, bool persist)
  signal exploreTabRequested(string tab)

  function maybeResyncBoot() {
    if (!bootResync || bootApplying || !bootResyncArmed) return
    if (bootApplied.length === 0 || bootAppliedTheme.length === 0 || bootCurrentTheme.length === 0) return
    if (bootAppliedTheme === bootCurrentTheme) return
    logEvent("boot-resync " + bootAppliedTheme + " -> " + bootCurrentTheme)
    if (bootApplied.indexOf("snapshot:") === 0) {
      // The fresh snapshot carries the new background too.
      bootBgResyncTimer.stop()
      bootResnapshotRequested(bootApplied.substring(9), bootSetting !== "follow")
      return
    }
    // Re-bake what is actually installed: with follow and a twin-less lock
    // design the setting resolves to nothing, but the installed twin still
    // needs the new colors.
    applyBoot(true, bootApplied)
  }

  // Cycling the background inside a theme leaves an applied snapshot showing
  // the old wallpaper. Retake it once the cycling settles; same opt-out as
  // the theme resync.
  property string bootLastBackground: ""

  onBackgroundPathChanged: {
    if (backgroundPath.length === 0) return
    if (bootLastBackground.length === 0) { bootLastBackground = backgroundPath; return }
    if (bootLastBackground === backgroundPath) return
    bootLastBackground = backgroundPath
    if (!bootResync || !bootResyncArmed) return
    if (bootApplied.indexOf("snapshot:") !== 0) return
    bootBgResyncTimer.restart()
  }

  Timer {
    id: bootBgResyncTimer
    interval: 8000
    onTriggered: {
      if (!root.bootResync || root.bootApplying) return
      if (root.bootApplied.indexOf("snapshot:") !== 0) return
      root.logEvent("boot-resync background")
      root.bootResnapshotRequested(root.bootApplied.substring(9), root.bootSetting !== "follow")
    }
  }


  // Thumbnails for the explorer's boot cards, one per option and theme,
  // rendered in the background by plymouth/previews.sh.
  property int bootPreviewsVersion: 0
  property bool bootPreviewsRunning: false

  property bool bootPreviewsPending: false

  function refreshBootPreviews() {
    refreshBootLists()
    // A request landing mid-run must queue a follow-up: the running pass
    // rendered the state before this change and would leave stale previews.
    if (bootPreviewsRunning) { bootPreviewsPending = true; return }
    bootPreviewsRunning = true
    bootPreviewsProc.command = ["bash", pluginDir + "/plymouth/previews.sh"]
    bootPreviewsProc.running = true
  }

  Process {
    id: bootPreviewsProc
    stdout: StdioCollector { }
    stderr: StdioCollector { }
    onExited: function(exitCode) {
      root.bootPreviewsRunning = false
      root.bootPreviewsVersion++
      if (root.bootPreviewsPending) {
        root.bootPreviewsPending = false
        root.refreshBootPreviews()
      }
    }
  }


  // The user's own clips and boot layouts, listed as cards on the boot tab.
  property var bootVideos: []
  property var bootCustomDesigns: []

  function refreshBootLists() { bootListsProc.running = true }

  Process {
    id: bootListsProc
    command: ["bash", "-c", "ls -1 \"$HOME/.config/omarchy/lock-videos\" 2>/dev/null; echo ::; for f in \"$HOME\"/.config/omarchy/boot-designs/*.conf; do [ -f \"$f\" ] && basename \"$f\" .conf; done"]
    stdout: StdioCollector {
      id: bootListsOut
      waitForEnd: true
      onStreamFinished: {
        var parts = String(bootListsOut.text || "").split("::")
        root.bootVideos = (parts[0] || "").split("\n")
          .map(function(l) { return l.trim() })
          .filter(function(l) { return /\.(mp4|mkv|webm|mov|m4v)$/i.test(l) })
        root.bootCustomDesigns = (parts.length > 1 ? parts[1] : "").split("\n")
          .map(function(l) { return l.trim() })
          .filter(function(l) { return l.length > 0 })
      }
    }
  }

  // How much of a clip boot screen plays, in seconds; 0 plays it all. Saved
  // on the plugin entry as bootClipSeconds.
  property int bootClipSecondsOverride: -1
  readonly property int configuredBootClipSeconds: {
    var cfg = root.settingsConfig
    var list = cfg && Array.isArray(cfg.plugins) ? cfg.plugins : []
    for (var i = 0; i < list.length; i++) {
      var entry = list[i]
      if (entry && String(entry.id || "") === pluginId && entry.bootClipSeconds !== undefined)
        return Math.max(0, Math.min(30, Number(entry.bootClipSeconds) || 0))
    }
    return 0
  }
  readonly property int bootClipSeconds: bootClipSecondsOverride >= 0 ? bootClipSecondsOverride : configuredBootClipSeconds

  function setBootClipSeconds(n) {
    var v = Math.max(0, Math.min(30, Math.round(Number(n) || 0)))
    bootClipSecondsOverride = v
    if (shell && typeof shell.updateEntryInline === "function") {
      var current = pluginEntry()
      if (v === 0) delete current.bootClipSeconds
      else current.bootClipSeconds = v
      writeEntry(current)
    }
    logEvent("boot-clip=" + v)
    var appliedIsClip = bootApplied.indexOf("video:") === 0
    if (!appliedIsClip) {
      var d = Designs.byId(bootApplied)
      appliedIsClip = !!(d && d.bootKind === "clip")
    }
    if (appliedIsClip) applyBoot(true, bootApplied)
    return true
  }

  // Plumbing for the boot layout editor in the explorer.
  signal bootDesignLoaded(string name, string content)
  property string bootDesignPendingLoad: ""

  function loadBootDesign(name) {
    bootDesignPendingLoad = String(name)
    bootLoadProc.command = ["cat", home + "/.config/omarchy/boot-designs/" + bootDesignPendingLoad + ".conf"]
    bootLoadProc.running = true
  }

  Process {
    id: bootLoadProc
    stdout: StdioCollector {
      id: bootLoadOut
      waitForEnd: true
      onStreamFinished: root.bootDesignLoaded(root.bootDesignPendingLoad, String(bootLoadOut.text || ""))
    }
  }

  property string bootSaveName: ""
  function saveBootDesign(name, content) {
    bootSaveName = String(name)
    bootSaveProc.command = ["bash", "-c",
      'mkdir -p "$HOME/.config/omarchy/boot-designs" && printf %s "$1" > "$HOME/.config/omarchy/boot-designs/$2.conf"',
      "--", String(content), String(name)]
    bootSaveProc.running = true
  }

  Process {
    id: bootSaveProc
    onExited: function(exitCode) {
      // Generate the matching lock screen QML + its preview from the same
      // layout, so the pair stays in sync.
      var previews = root.stateHome + "/omarchy/lock-explorer-boot-previews"
      lockGenProc.command = ["bash", "-c",
        'mkdir -p "$3" && bash "$1" "$HOME/.config/omarchy/boot-designs/$2.conf" "$2" "$3/custom-$2-lock-$4.png"',
        "--", root.pluginDir + "/plymouth/custom/genlock.sh", root.bootSaveName, previews, root.bootCurrentTheme]
      lockGenProc.running = true
    }
  }

  Process {
    id: lockGenProc
    onExited: function(exitCode) {
      root.refreshBootPreviews()
      root.rescanUserDesigns()
      root.logEvent("boot-design-saved")
    }
  }

  function createBootDesign() {
    bootCreateProc.command = ["bash", "-c",
      'dir="$HOME/.config/omarchy/boot-designs"; mkdir -p "$dir"; n="my-boot"; i=2; while [ -f "$dir/$n.conf" ]; do n="my-boot-$i"; i=$((i+1)); done; cp "$1" "$dir/$n.conf"; echo "$n"',
      "--", pluginDir + "/plymouth/custom/template.conf"]
    bootCreateProc.running = true
  }

  Process {
    id: bootCreateProc
    stdout: StdioCollector {
      id: bootCreateOut
      waitForEnd: true
      onStreamFinished: {
        var n = String(bootCreateOut.text || "").trim()
        if (n.length > 0) {
          root.refreshBootLists()
          root.loadBootDesign(n)
        }
      }
    }
  }


  // Boot screen rotation: a set of options that advances one step after
  // every boot. The screen is baked into the boot image, so the swap happens
  // in the background right after login -- silently through the root path
  // unit rotate-setup.sh installs (one pkexec, once), with a normal polkit
  // prompt as the fallback. Saved on the plugin entry as bootRotation.
  property var bootRotationOverride: null
  readonly property var configuredBootRotation: {
    var cfg = root.settingsConfig
    var list = cfg && Array.isArray(cfg.plugins) ? cfg.plugins : []
    for (var i = 0; i < list.length; i++) {
      var entry = list[i]
      if (entry && String(entry.id || "") === pluginId && Array.isArray(entry.bootRotation)) return entry.bootRotation
    }
    return []
  }
  readonly property var bootRotation: bootRotationOverride !== null ? bootRotationOverride : configuredBootRotation
  property bool bootRotateSilent: false
  property string bootRotateNext: ""

  function toggleBootRotation(id) {
    var v = String(id || "")
    if (v.length === 0 || v === "stock") return false
    var list = bootRotation.slice()
    var idx = list.indexOf(v)
    if (idx === -1) list.push(v)
    else list.splice(idx, 1)
    bootRotationOverride = list
    if (shell && typeof shell.updateEntryInline === "function") {
      var current = pluginEntry()
      if (list.length === 0) delete current.bootRotation
      else current.bootRotation = list
      writeEntry(current)
    }
    logEvent("boot-rotation=" + list.join(","))
    return true
  }

  // persist=false applies the snapshot as the on-disk artifact but leaves the
  // saved boot setting alone (used by follow mode, which stays "follow").
  // entryRect ("cx,cy,w,h" in percent) is where the design's own input box
  // sits in the snapshot; the boot theme puts its bullets there.
  function applyBootSnapshot(id, persist, entryRect) {
    if (bootApplying) return
    if (persist === undefined) persist = true
    if (persist) {
      bootOverride = "snapshot:" + id
      if (shell && typeof shell.updateEntryInline === "function") {
        var current = pluginEntry()
        current.boot = "snapshot:" + id
        writeEntry(current)
      }
    }
    bootApplying = true
    bootApplyTarget = "snapshot:" + id
    bootApplyProc.command = ["env", "BOOT_CLIP_SECONDS=0",
      "SNAPSHOT_ENTRY=" + String(entryRect || ""),
      "bash", pluginDir + "/plymouth/apply.sh", "snapshot:" + id]
    bootApplyProc.running = true
    logEvent("boot-snapshot=" + id + (persist ? "" : " (follow)"))
  }

  function enableBootRotation() {
    if (bootRotateSilent) { setBoot("rotate"); return }
    // No home argument: the setup script reads it from the account that
    // authenticated to pkexec. It lands in a root-owned helper, and $HOME is
    // whatever the process that started the shell set it to.
    bootRotateSetupProc.command = ["pkexec", "bash", pluginDir + "/plymouth/rotate-setup.sh", "install"]
    bootRotateSetupProc.running = true
  }

  Process {
    id: bootRotateSetupProc
    stdout: StdioCollector { }
    stderr: StdioCollector { }
    onExited: function(exitCode) {
      if (exitCode === 0) {
        root.bootRotateSilent = true
        root.setBoot("rotate")
        root.logEvent("boot-rotate-setup=ok")
      } else {
        root.logEvent("boot-rotate-setup-failed=" + exitCode)
      }
    }
  }

  Process {
    id: bootRotateCheckProc
    command: ["bash", "-c", "[ -f /etc/systemd/system/omarchy-lock-explorer-boot.path ] && echo yes || echo no"]
    stdout: StdioCollector {
      id: bootRotateCheckOut
      waitForEnd: true
      onStreamFinished: {
        root.bootRotateSilent = String(bootRotateCheckOut.text || "").trim() === "yes"
        root.maybeRotateBoot()
      }
    }
  }

  // Advance at most once per boot: the stamp file keeps the boot id, so
  // shell restarts inside the same boot leave the rotation alone.
  function maybeRotateBoot() {
    if (bootSetting !== "rotate") return
    var list = bootRotation
    if (!list || list.length === 0) return
    var idx = list.indexOf(bootApplied)
    var next = list[(idx + 1) % list.length]
    if (next === bootApplied) return
    bootRotateNext = next
    bootRotateStampProc.running = true
  }

  Process {
    id: bootRotateStampProc
    command: ["bash", "-c", "bid=$(cat /proc/sys/kernel/random/boot_id); f=\"$HOME/.local/state/omarchy/lock-explorer-boot-rotated\"; [ \"$(cat \"$f\" 2>/dev/null)\" = \"$bid\" ] && echo skip || { echo \"$bid\" > \"$f\"; echo go; }"]
    stdout: StdioCollector {
      id: bootRotateStampOut
      waitForEnd: true
      onStreamFinished: {
        if (String(bootRotateStampOut.text || "").trim() !== "go") return
        if (root.bootRotateNext.length === 0 || root.bootApplying) return
        root.logEvent("boot-rotate -> " + root.bootRotateNext)
        if (root.bootRotateSilent) {
          root.bootApplying = true
          root.bootApplyTarget = root.bootRotateNext
          bootApplyProc.command = ["env", "BOOT_CLIP_SECONDS=" + root.bootClipSeconds, "bash", root.pluginDir + "/plymouth/apply.sh", root.bootRotateNext, "--spool"]
          bootApplyProc.running = true
        } else {
          root.applyBoot(true, root.bootRotateNext)
        }
      }
    }
  }

  // Armed a little after startup so a stale palette right after login gets
  // one polkit prompt once the desktop has settled, not mid-splash.
  property bool bootResyncArmed: false

  Timer {
    interval: 12000
    running: true
    onTriggered: {
      root.bootResyncArmed = true
      root.refreshBootLists()
      // The silent-rotation check chains into maybeRotateBoot; the resync
      // runs after so a rotation that just advanced satisfies it.
      bootRotateCheckProc.running = true
      root.maybeResyncBoot()
    }
  }

  FileView {
    path: root.stateHome + "/omarchy/current/theme.name"
    watchChanges: true
    printErrors: false
    onLoaded: { root.bootCurrentTheme = String(text()).trim(); root.maybeResyncBoot() }
    onLoadFailed: root.bootCurrentTheme = ""
    onFileChanged: reload()
  }

  function setDesign(id) {
    var d = Designs.byId(String(id || ""))
    if (!d) return false
    designOverride = d.id
    if (shell && typeof shell.updateEntryInline === "function") {
      var current = pluginEntry()
      current.design = d.id
      writeEntry(current)
    }
    logEvent("design=" + d.id)
    if (bootSetting === "follow") applyBoot(false)
    return true
  }

  readonly property string home: Quickshell.env("HOME")
  readonly property string stateHome: home + "/.local/state"
  readonly property string userName: Quickshell.env("USER") || Quickshell.env("LOGNAME")
  readonly property string currentBackgroundLink: stateHome + "/omarchy/current/background"

  property bool lockRequested: false
  property bool pendingSessionLock: false
  property bool authenticatingPassword: false
  property bool fingerprintAuthenticating: false
  property bool faceAuthenticating: false
  property bool passwordPamConfigured: false
  property bool fingerprintConfigured: false
  property bool faceConfigured: false
  // Security-key unlock is a mode the user is in. It does not run alongside
  // the password: pam_u2f gets its own PAM service and its own context, since
  // sharing omarchy-lock-password would send every mistyped password to the
  // key as a PIN attempt, and a key locks itself out after eight.
  property string authMode: "password" // "password" | "fido2"
  // Picked once per lock by the probe; a later probe never overrides a choice.
  property bool authModeSettled: false
  // What the probe found, and what the lock screen does with it. Everything
  // downstream reads fido2Configured, so the Settings toggle only has to turn
  // this one property off.
  property bool fido2Installed: false
  readonly property bool fido2Configured: fido2Installed && fido2Enabled
  property bool fido2TokenPresent: false
  property bool fido2Authenticating: false
  property bool fido2NeedsPin: false
  property string fido2Status: ""
  // Last thing pam_u2f said that did not want an answer, i.e. the cue.
  property string fido2Cue: ""
  // PIN attempts in this lock. A wrong PIN costs one of the key's retries,
  // and the key itself refuses further PINs after three in a row until it is
  // replugged, so the screen stops offering it at the same point and asks for
  // the password. Attempts that never reached a PIN (no touch, no key) are
  // free and do not count.
  property int fido2PinAttempts: 0
  property bool fido2PinSubmitted: false
  readonly property int fido2PinAttemptLimit: 3
  readonly property bool fido2Exhausted: fido2PinAttempts >= fido2PinAttemptLimit
  readonly property bool fido2Active: authMode === "fido2"
  property bool previewVisible: false
  property string enteredPassword: ""
  property string pendingPassword: ""
  property string failureMessage: ""
  property int failedAttempts: 0
  property string backgroundPath: ""
  property int backgroundVersion: 0
  property string lastEvent: "init"
  property string lastEventAt: ""
  property bool strandedLock: false
  property bool strandedLockResolved: false
  property bool unlocking: false
  // The clip designs (Storm, Eyes, ...) hold the lock surface while their
  // video plays through as the unlock. unlockPlayback reaches the design,
  // clipUnlocking is the hold, clipFailsafe the way out if the file misbehaves.
  property bool clipUnlocking: false
  property bool unlockPlayback: false
  property bool previewClipPlaying: false
  // Nothing should decode video into a screen that is switched off.
  property bool screenBlanked: false

  // With `misc:session_lock_xray` the compositor keeps drawing the desktop
  // under the lock surface, so the unlock fades straight into it and the
  // wallpaper the animation otherwise lands on would only be in the way.
  property bool sessionLockXray: false

  readonly property bool locked: lockRequested || sessionLock.locked || sessionLock.secure
  readonly property bool authenticating: authenticatingPassword || fingerprintAuthenticating || faceAuthenticating || fido2Authenticating

  function realScreenCount() {
    var screens = Quickshell.screens || []
    var count = 0

    for (var i = 0; i < screens.length; i++) {
      var screen = screens[i]
      if (screen && screen.name && screen.width > 0 && screen.height > 0) count += 1
    }

    return count
  }

  function hasRealScreen() {
    return realScreenCount() > 0
  }

  function queueSessionLock() {
    pendingSessionLock = true
    if (!sessionLockStabilizeTimer.running) logEvent("lock-pending: screen-stabilizing")
    sessionLockStabilizeTimer.restart()
    if (!pendingSessionLockTimer.running) pendingSessionLockTimer.start()
  }

  function requestSessionLock() {
    if (!lockRequested || sessionLock.locked || sessionLock.secure) return
    if (sessionLockStabilizeTimer.running) return

    if (!hasRealScreen()) {
      if (!pendingSessionLock || lastEvent !== "lock-pending: no-real-screen") logEvent("lock-pending: no-real-screen")
      pendingSessionLock = true
      if (!pendingSessionLockTimer.running) pendingSessionLockTimer.start()
      return
    }

    pendingSessionLock = false
    pendingSessionLockTimer.stop()
    sessionLock.locked = true
  }

  // ext-session-lock outlives its client, and a restart carries no lock over, so
  // a session locked this early is an orphan behind Hyprland's failsafe. Outputs
  // are often still absent here, so ask until the answer means something.
  function checkStrandedLock() {
    if (strandedLockResolved || strandedLockCheckProc.running) return

    // A lock this shell took is nobody's orphan.
    if (locked || lockRequested) {
      strandedLockResolved = true
      return
    }

    strandedLockCheckProc.running = true
  }

  function recoverStrandedLock() {
    if (!strandedLock || locked || !passwordPamConfigured) return

    strandedLock = false
    logEvent("lock-stranded: recovering")
    beginLock()
  }

  function refreshBackground() {
    if (!readlinkProc.running) readlinkProc.running = true
  }

  function refreshFingerprintStatus() {
    if (!fingerprintCheckProc.running) fingerprintCheckProc.running = true
  }

  function refreshFaceStatus() {
    if (!faceCheckProc.running) faceCheckProc.running = true
  }

  function refreshFido2Status() {
    if (!fido2CheckProc.running) fido2CheckProc.running = true
  }

  function refreshSessionLockXray() {
    if (!sessionLockXrayProc.running) sessionLockXrayProc.running = true
  }

  function logEvent(event) {
    lastEvent = event
    lastEventAt = new Date().toISOString()
    console.log("omarchy lock " + lastEventAt + " " + event)
  }

  function resetAuthenticationState() {
    enteredPassword = ""
    pendingPassword = ""
    failureMessage = ""
    failedAttempts = 0
    authenticatingPassword = false
    fingerprintAuthenticating = false
    fingerprintRetryTimer.stop()
    if (passwordPam.active) passwordPam.abort()
    if (fingerprintPam.active) fingerprintPam.abort()
    if (facePam.active) facePam.abort()
    abortFido2()
    fido2PinAttempts = 0
    fido2PinSubmitted = false
    authMode = "password"
    authModeSettled = false
  }

  function beginLock() {
    if (!passwordPamConfigured) {
      logEvent("lock-denied: missing-pam")
      return false
    }

    cancelUnlockAnimation()
    resetAuthenticationState()
    lockRequested = true
    armBlankTimer()
    logEvent("lock-requested")
    queueSessionLock()

    Qt.callLater(function() {
      root.refreshBackground()
      root.refreshFingerprintStatus()
      root.refreshFaceStatus()
      root.refreshFido2Status()
      root.refreshSessionLockXray()
      root.rescanUserDesigns()
      // The frame is ready before the unlock needs it.
      if (root.designHasClip) root.prepareClipWallpaper(root.designClipPath)
      else if (root.stingPath.length > 0) root.prepareClipWallpaper(root.stingPath)
    })

    return true
  }

  function finishUnlock() {
    if (!root.locked && !lockRequested) return
    if (unlocking || clipUnlocking) return

    lockRequested = false
    pendingSessionLock = false
    sessionLockStabilizeTimer.stop()
    pendingSessionLockTimer.stop()
    resetAuthenticationState()
    idleBlankTimer.stop()
    runWake()

    // The surface has to stay up while it animates away -- dropping the lock
    // first takes the screen with it. The timer also releases the lock if the
    // animation never runs, so nothing can leave the session stuck behind it.
    // A clip design keeps the surface and plays its video as the unlock. The
    // design says when it is done; the failsafe does not care what it thinks.
    // Without qt6-multimedia a clip design has already fallen back to
    // Classic, so unlock instantly instead of waiting on the clip failsafe.
    if (designHasClip && multimediaAvailable && (sessionLock.locked || sessionLock.secure)) {
      clipUnlocking = true
      unlockPlayback = true
      clipFailsafe.restart()
      // Second chance for designs picked while already locked.
      if (preparedClipWallpaper.length === 0) prepareClipWallpaper(designClipPath)
      logEvent("unlocking=clip")
      return
    }

    if (unlockAnimated && (sessionLock.locked || sessionLock.secure)) {
      unlocking = true
      logEvent("unlocking=" + unlockAnimation)
      unlockTimer.restart()
      return
    }

    releaseLock()
  }

  function releaseLock() {
    unlockTimer.stop()
    clipFailsafe.stop()
    var hadClip = clipUnlocking
    clipUnlocking = false
    unlockPlayback = false
    unlocking = false
    sessionLock.locked = false
    logEvent("unlocked")
    // The clip was the whole show, no second video on top of it.
    if (hadClip) commitClipWallpaper()
    else playSting()
  }

  function cancelUnlockAnimation() {
    if (!unlocking && !clipUnlocking) return
    unlockTimer.stop()
    clipFailsafe.stop()
    unlocking = false
    clipUnlocking = false
    unlockPlayback = false
    logEvent("unlock-cancelled")
  }

  function armBlankTimer() {
    idleBlankTimer.armedAt = Date.now()
    idleBlankTimer.restart()
  }

  function runWake() {
    screenBlanked = false
    if (!wakeProcess.running) wakeProcess.running = true
    if (lockRequested) armBlankTimer()
  }

  function runBlank() {
    if (keepDisplayOn) return
    screenBlanked = !displayBlankingSuppressed
    if (!blankProcess.running) blankProcess.running = true
  }

  function submitPassword(value) {
    var password = String(value || "")
    // A password submitted while on the key (emergency field, custom design)
    // is a password: leave key mode first so it never meets fido2Pam.
    if (password.length > 0 && fido2Active) setAuthMode("password")
    if (!lockRequested || authenticatingPassword || password.length === 0) {
      if (password.length === 0 && faceConfigured) root.startFace()
      return
    }

    runWake()
    pendingPassword = password
    failureMessage = ""
    authenticatingPassword = true

    if (!passwordPam.start()) {
      handlePasswordFailure()
      return
    }

    Qt.callLater(respondToPasswordPrompt)
  }

  function respondToPasswordPrompt() {
    if (!authenticatingPassword || !passwordPam.active || !passwordPam.responseRequired) return
    passwordPam.respond(pendingPassword)
  }

  function handlePasswordFailure() {
    if (!lockRequested) return

    authenticatingPassword = false
    enteredPassword = ""
    pendingPassword = ""
    failedAttempts += 1
    failureMessage = "Authentication failed (" + failedAttempts + ")"
    runWake()
  }

  function startFingerprint() {
    if (!lockRequested || !sessionLock.secure || !fingerprintConfigured) return
    if (fingerprintPam.active || fingerprintAuthenticating) return

    fingerprintAuthenticating = true
    if (!fingerprintPam.start()) {
      fingerprintAuthenticating = false
    }
  }

  function handleFingerprintFinished(result) {
    fingerprintAuthenticating = false

    if (!lockRequested) return
    if (result === PamResult.Success) {
      finishUnlock()
    } else if (fingerprintConfigured) {
      fingerprintRetryTimer.restart()
    }
  }

  function startFace() {
    if (!lockRequested || !sessionLock.secure || !faceConfigured) return
    if (facePam.active || faceAuthenticating) return

    faceAuthenticating = true
    if (!facePam.start()) faceAuthenticating = false
  }

  function handleFaceFinished(result) {
    faceAuthenticating = false

    if (!lockRequested) return
    if (result === PamResult.Success) {
      finishUnlock()
    } else if (faceConfigured) {
      faceRetryTimer.restart()
    }
  }

  // Key mode when a key is enrolled and plugged in at lock time, password
  // otherwise. Runs from the probe, not beginLock(): the answer is a
  // subprocess away when the lock is requested.
  function settleAuthMode() {
    if (authModeSettled || !lockRequested) return
    if (!fido2Configured || !fido2TokenPresent || fido2Exhausted) return
    if (authenticatingPassword || enteredPassword.length > 0) return
    setAuthMode("fido2")
  }

  function setAuthMode(mode) {
    if (mode !== "password" && mode !== "fido2") return
    // Never yank the field while a password check is in flight.
    if (authenticatingPassword) return
    var switching = authMode !== mode && !(mode === "fido2" && !fido2Configured)
    if (switching) {
      if (authMode === "fido2") abortFido2()
      authMode = mode
      failureMessage = ""
      enteredPassword = ""
      pendingPassword = ""
      logEvent("auth-mode=" + mode)
    }
    // Settled last: the hotplug watcher runs on (!settled || fido2Active),
    // and settling before the switch would stop and respawn it on every Tab.
    // A request for a mode that cannot be entered still settles, so a later
    // probe does not override what the user asked for.
    authModeSettled = true
  }

  // Glyph click, Tab from the password, Enter on the inert field: switch to
  // the key if not there yet, then look for it again and start. The probe's
  // onExited does the starting.
  function requestFido2() {
    if (!lockRequested || !fido2Configured) return
    if (fido2Exhausted) {
      failureMessage = "Use your password"
      return
    }
    if (!fido2Active) setAuthMode("fido2")
    if (!fido2Active || fido2Authenticating) return
    failureMessage = ""
    fido2Status = "Looking for your key…"
    refreshFido2Status()
  }

  function startFido2() {
    // Same gate as startFingerprint: a success before the surface is secure
    // would unlock a lock that never took.
    if (!lockRequested || !sessionLock.secure) return
    if (!fido2Active || !fido2Configured || fido2Exhausted) return
    if (fido2Pam.active || fido2Authenticating) return
    if (!fido2TokenPresent) {
      fido2Status = "No security key found"
      return
    }

    runWake()
    // Anything typed before the key was found would be read-only in the field
    // from here on and end up in front of the PIN, costing a retry for nothing.
    enteredPassword = ""
    fido2NeedsPin = false
    fido2PinSubmitted = false
    fido2Cue = ""
    fido2Status = "Waiting for your key…"
    fido2Authenticating = true
    if (!fido2Pam.start()) {
      fido2Authenticating = false
      fido2Status = "Could not start the key"
    }
  }

  function abortFido2() {
    // Cleared before abort(): whatever completed/error the abort emits is
    // then ignored by handleFido2Finished instead of counted as a failure.
    fido2Authenticating = false
    fido2NeedsPin = false
    fido2Status = ""
    fido2Cue = ""
    if (fido2Pam.active) fido2Pam.abort()
  }

  // pam_u2f asks for the PIN with a prompt that wants a response and announces
  // the touch with one that does not. The kind decides, never the wording.
  // It says nothing after a PIN is accepted, even though it then waits for a
  // touch, so the cue it did send is kept and put back at that point.
  function handleFido2Message() {
    if (!fido2Authenticating) return
    runWake()

    if (fido2Pam.responseRequired) {
      fido2NeedsPin = true
      fido2Status = "Enter the PIN for your key"
      return
    }

    fido2NeedsPin = false
    var text = String(fido2Pam.message || "").trim()
    if (text.length > 0) {
      fido2Cue = text
      fido2Status = text
    }
  }

  function submitFido2Pin(pin) {
    var value = String(pin || "")
    if (!fido2Active || !fido2Authenticating || value.length === 0) return
    if (!fido2Pam.active || !fido2Pam.responseRequired) return

    fido2Pam.respond(value)
    fido2NeedsPin = false
    fido2PinSubmitted = true
    enteredPassword = ""
    // Not "Checking…": the key is lit and waiting for a finger, and the only
    // other sign of that is the light on the key itself.
    fido2Status = fido2Cue.length > 0 ? fido2Cue : "Touch your security key"
  }

  function handleFido2Finished(result) {
    if (!fido2Authenticating) return

    fido2Authenticating = false
    fido2NeedsPin = false
    enteredPassword = ""

    if (!lockRequested) return
    if (result === PamResult.Success) {
      finishUnlock()
      return
    }

    // No retry timer, unlike fingerprint and face: a failed key attempt can
    // have cost one of its PIN retries, so the next one is the user's call.
    failedAttempts += 1
    if (fido2PinSubmitted) fido2PinAttempts += 1
    fido2PinSubmitted = false
    fido2Status = ""
    if (fido2Exhausted) {
      // Three PINs went to the key in this lock. Whether they were wrong or a
      // bystander was guessing, the key is at its own limit and the rest of
      // its retries are not the lock screen's to spend. Back to the password;
      // setAuthMode clears failureMessage, so the message goes after it.
      setAuthMode("password")
      failureMessage = "Use your password"
      logEvent("fido2-exhausted")
    } else {
      failureMessage = "Security key failed (" + failedAttempts + ")"
    }
    runWake()
  }

  WlSessionLock {
    id: sessionLock

    locked: false

    onSecureStateChanged: {
      root.logEvent("secure=" + secure)
      if (secure) {
        root.pendingSessionLock = false
        sessionLockStabilizeTimer.stop()
        pendingSessionLockTimer.stop()
        root.startFingerprint()
        root.startFido2()
      }
    }

    onLockStateChanged: {
      root.logEvent("session-locked=" + locked)

      if (locked) {
        root.pendingSessionLock = false
        sessionLockStabilizeTimer.stop()
        pendingSessionLockTimer.stop()
      }

      if (!locked && root.lockRequested) {
        root.lockRequested = false
        root.pendingSessionLock = false
        sessionLockStabilizeTimer.stop()
        pendingSessionLockTimer.stop()
        root.resetAuthenticationState()
        root.runWake()
      }
    }

    WlSessionLockSurface {
      id: lockSurface
      color: Color.background

      UnlockLayer {
        anchors.fill: parent
        animation: root.unlockAnimation
        duration: root.unlockDuration
        active: root.unlocking
        backgroundUrl: root.locked && !root.sessionLockXray ? root.backgroundUrl : ""

        LockHost {
          id: lockView
          anchors.fill: parent
          fadeIn: true
          designId: root.showsInput(lockSurface.screen) ? root.designId : "companion"
          revision: root.designsRevision
          backgroundPath: root.backgroundPath
          backgroundVersion: root.backgroundVersion
          avatarPath: root.avatarPath
          avatarVersion: root.avatarVersion
          fingerprintConfigured: root.fingerprintConfigured
          faceConfigured: root.faceConfigured
          fido2Configured: root.fido2Configured
          fido2Active: root.fido2Active
          fido2Authenticating: root.fido2Authenticating
          fido2NeedsPin: root.fido2NeedsPin
          fido2Status: root.fido2Status
          authenticatingPassword: root.authenticatingPassword
          failureMessage: root.failureMessage
          failedAttempts: root.failedAttempts
          inputEnabled: root.lockRequested
          loadBackground: root.locked
          passwordText: root.enteredPassword
          videoPath: root.videoPath
          videoPlaying: root.locked && !root.screenBlanked
          unlockPlayback: root.unlockPlayback && root.showsInput(lockSurface.screen)
          clipSpeed: root.clipSpeed
          twelveHour: root.twelveHour
          onUnlockFinished: root.releaseLock()
          onPasswordTextEdited: function(password) { root.enteredPassword = password }
          onSubmitPassword: function(password) { root.submitPassword(password) }
          onClearFailureRequested: root.failureMessage = ""
          onWakeRequested: root.runWake()
          onFaceRequested: root.startFace()
          onFido2Requested: root.requestFido2()
          onPasswordRequested: root.setAuthMode("password")
          onSubmitFido2Pin: function(pin) { root.submitFido2Pin(pin) }
        }
      }
    }
  }

  PanelWindow {
    id: previewWindow
    visible: root.previewVisible
    anchors { top: true; bottom: true; left: true; right: true }
    color: "transparent"
    WlrLayershell.namespace: "omarchy-lock-explorer-preview"
    WlrLayershell.layer: WlrLayer.Overlay
    WlrLayershell.keyboardFocus: WlrKeyboardFocus.Exclusive
    exclusionMode: ExclusionMode.Ignore

    UnlockLayer {
      anchors.fill: parent
      animation: root.unlockAnimation
      duration: root.unlockDuration
      active: root.previewUnlocking

      LockHost {
        anchors.fill: parent
        designId: root.previewDesignId.length > 0 ? root.previewDesignId : root.designId
        revision: root.designsRevision
        backgroundPath: root.backgroundPath
        backgroundVersion: root.backgroundVersion
        avatarPath: root.avatarPath
        avatarVersion: root.avatarVersion
        fingerprintConfigured: root.fingerprintConfigured
        authenticatingPassword: false
        failureMessage: root.previewFailure
        failedAttempts: root.previewFailure.length > 0 ? 1 : 0
        inputEnabled: root.previewVisible && !root.previewUnlocking
        loadBackground: root.previewVisible
        passwordText: root.previewTyped
        videoPath: root.videoPath
        videoPlaying: root.previewVisible
        faceConfigured: root.faceConfigured
        fido2Configured: root.fido2Configured
        unlockPlayback: root.previewClipPlaying
        clipSpeed: root.clipSpeed
        twelveHour: root.twelveHour
        // Hold the clip's last frame in the preview instead of snapping back
        // to the start; Esc (hidePreview) resets it.
        onUnlockFinished: {}
        onPasswordTextEdited: function(password) { root.previewTyped = password }
        onFaceRequested: root.startFace()
      }
    }

    MouseArea {
      anchors.fill: parent
      acceptedButtons: Qt.LeftButton | Qt.RightButton
      onClicked: { root.previewVisible = false; root.previewDesignId = "" }
    }
  }

  // The unlock clip window (see StingWindow.qml). It is the only part of the
  // service that touches QtMultimedia, so it loads through this Loader:
  // without qt6-multimedia the load fails, the video features switch off and
  // the rest of the service — the lock screen and the `lock` IPC target —
  // carries on. Importing QtMultimedia at the top of this file instead would
  // take the whole service down with a bare "Target not found".
  readonly property bool multimediaAvailable: stingLoader.status === Loader.Ready

  Loader {
    id: stingLoader
    source: Qt.resolvedUrl("StingWindow.qml")
    onLoaded: item.lock = root
    onStatusChanged: {
      if (status === Loader.Error) {
        console.warn("lock-explorer: qt6-multimedia is not installed; video designs and unlock clips are disabled."
          + " Install it with `sudo pacman -S qt6-multimedia`, then run `omarchy restart shell`.")
      }
    }
  }

  PamContext {
    id: passwordPam
    config: "omarchy-lock-password"
    user: root.userName

    onResponseRequiredChanged: root.respondToPasswordPrompt()
    onPamMessage: root.respondToPasswordPrompt()

    onCompleted: function(result) {
      root.authenticatingPassword = false
      root.pendingPassword = ""

      if (!root.lockRequested) return
      if (result === PamResult.Success) root.finishUnlock()
      else root.handlePasswordFailure()
    }

    onError: function(error) {
      root.handlePasswordFailure()
    }
  }

  PamContext {
    id: fingerprintPam
    config: "omarchy-lock-fingerprint"
    user: root.userName

    onCompleted: function(result) {
      root.handleFingerprintFinished(result)
    }

    onError: function(error) {
      root.fingerprintAuthenticating = false
      if (root.lockRequested && root.fingerprintConfigured) fingerprintRetryTimer.restart()
    }
  }

  PamContext {
    id: facePam
    config: "omarchy-lock-face"
    user: root.userName

    onCompleted: function(result) {
      root.handleFaceFinished(result)
    }

    onError: function(error) {
      root.faceAuthenticating = false
      if (root.lockRequested && root.faceConfigured) faceRetryTimer.restart()
    }
  }

  PamContext {
    id: fido2Pam
    config: "omarchy-lock-fido2"
    user: root.userName

    onResponseRequiredChanged: root.handleFido2Message()
    onPamMessage: root.handleFido2Message()

    onCompleted: function(result) {
      root.handleFido2Finished(result)
    }

    onError: function(error) {
      root.handleFido2Finished(PamResult.Error)
    }
  }

  Timer {
    id: unlockTimer
    interval: Math.max(1, root.unlockDuration + 80)
    repeat: false
    onTriggered: root.releaseLock()
  }

  // However long the clip claims to be, the screen comes back.
  Timer {
    id: clipFailsafe
    interval: 15000
    repeat: false
    onTriggered: root.releaseLock()
  }

  Timer {
    id: fingerprintRetryTimer
    interval: 250
    repeat: false
    onTriggered: root.startFingerprint()
  }

  Timer {
    id: faceRetryTimer
    interval: 250
    repeat: false
    onTriggered: root.startFace()
  }

  // A key plugged in after the lock came up has to be noticed, and polling for
  // it would mean spawning a probe every couple of seconds for as long as the
  // screen is locked. This sleeps on the udev netlink socket instead and wakes
  // only when a hidraw device actually appears. It exists in one state only:
  // locked, a key enrolled, none attached, nothing in flight, and the user is
  // not already on the password. A key arriving mid-password changes nothing
  // (settleAuthMode leaves half-typed input alone), so the process would be
  // kept alive for an answer nobody acts on.
  Process {
    id: fido2HotplugWatch
    running: root.lockRequested && root.fido2Configured && !root.fido2TokenPresent
      && !root.fido2Exhausted
      && !root.fido2Authenticating && !root.authenticatingPassword
      && root.enteredPassword.length === 0
      && (!root.authModeSettled || root.fido2Active)
    command: ["udevadm", "monitor", "--udev", "--subsystem-match=hidraw"]
    stdout: SplitParser {
      // "UDEV  [123.4] add  /devices/.../hidraw/hidraw0 (hidraw)"
      onRead: function(line) {
        if (String(line).indexOf(" add ") >= 0) fido2HotplugSettle.restart()
      }
    }
  }

  // udev announces the device before its permissions are in place, so give the
  // node a moment before asking whether libfido2 can open it. Also collapses
  // the burst of events one plug produces into a single probe.
  Timer {
    id: fido2HotplugSettle
    interval: 400
    repeat: false
    onTriggered: root.refreshFido2Status()
  }

  Process {
    id: userDesignsProc
    // Files built on ClipDesign are tagged (with their clip file when it is
    // named inline) so they keep their animation flag and their video across
    // rescans.
    command: ["bash", "-c", "for f in \"$0\"/*.qml; do [ -e \"$f\" ] || continue; c=$(grep -o 'clipName: \"[^\"]*\"' \"$f\" 2>/dev/null | head -1 | cut -d'\"' -f2); if [ -n \"$c\" ]; then printf '%s\\tclip\\t%s\\n' \"$f\" \"$c\"; elif grep -q ClipDesign \"$f\" 2>/dev/null; then printf '%s\\tclip\\n' \"$f\"; elif grep -q '// designer:1:' \"$f\" 2>/dev/null; then printf '%s\\tdesigner\\n' \"$f\"; else printf '%s\\n' \"$f\"; fi; done", root.userDesignsDir]
    stdout: StdioCollector {
      waitForEnd: true
      onStreamFinished: {
        var lines = String(text || "").split("\n").filter(function(l) { return l.trim().length > 0 })
        var list = lines.map(function(l) {
          var parts = l.split("\t")
          var d = Designs.fromUserFile(parts[0].trim())
          if (parts.length > 1 && parts[1].trim() === "clip") { d.anim = true; d.clip = true }
          // Made in the designer: E opens it there instead of in the code editor.
          if (parts.length > 1 && parts[1].trim() === "designer") {
            d.designer = true
            d.description = "Made in the designer"
          }
          if (parts.length > 2 && parts[2].trim().length > 0) d.clipFile = parts[2].trim()
          return d
        })
        var before = JSON.stringify(Designs.USER)
        Designs.setUser(list)
        if (JSON.stringify(list) !== before) root.designsRevision += 1
      }
    }
  }

  Process {
    id: readlinkProc
    command: ["readlink", "-f", root.currentBackgroundLink]
    stdout: StdioCollector {
      waitForEnd: true
      onStreamFinished: {
        var next = String(text || "").trim()
        if (next !== root.backgroundPath) {
          root.backgroundPath = next
          root.backgroundVersion += 1
        }
      }
    }
  }

  // The background is a symlink retarget with no file content to watch, so
  // poll it the way the stock background plugin does. Keeps backgroundPath
  // live for the boot-screen background resync.
  Timer {
    interval: 5000
    running: true
    repeat: true
    onTriggered: root.refreshBackground()
  }

  Process {
    id: fingerprintCheckProc
    command: ["bash", "-c", "if [[ -f /etc/pam.d/omarchy-lock-fingerprint ]] && command -v fprintd-list >/dev/null 2>&1 && fprintd-list \"$USER\" 2>/dev/null | grep -qi finger; then echo yes; else echo no; fi"]
    stdout: StdioCollector { id: fingerprintCheckStdout; waitForEnd: true }
    onExited: {
      root.fingerprintConfigured = String(fingerprintCheckStdout.text || "").trim() === "yes"
      if (root.lockRequested && root.fingerprintConfigured) root.startFingerprint()
      else if (!root.fingerprintConfigured && fingerprintPam.active) fingerprintPam.abort()
    }
  }

  Process {
    id: faceCheckProc
    command: [root.checkFaceAuthPath, "--yes"]
    stdout: StdioCollector { id: faceCheckStdout; waitForEnd: true }
    onExited: {
      root.faceConfigured = String(faceCheckStdout.text || "").trim() === "yes"
      if (root.lockRequested && root.faceConfigured) root.startFace()
      else if (!root.faceConfigured && facePam.active) facePam.abort()
    }
  }

  Process {
    id: fido2CheckProc
    command: [root.checkFido2AuthPath]
    stdout: StdioCollector { id: fido2CheckStdout; waitForEnd: true }
    onExited: {
      var answer = String(fido2CheckStdout.text || "").trim().split(/\s+/)
      root.fido2Installed = answer[0] === "yes"
      root.fido2TokenPresent = answer[1] === "present"

      if (!root.fido2Configured) {
        if (root.fido2Active) root.setAuthMode("password")
        return
      }

      if (!root.lockRequested) return
      if (!root.fido2Active) root.settleAuthMode()
      if (root.fido2Active) root.startFido2()
    }
  }

  Process {
    id: sessionLockXrayProc
    command: ["hyprctl", "getoption", "misc:session_lock_xray", "-j"]
    stdout: StdioCollector {
      id: sessionLockXrayOut
      waitForEnd: true
      onStreamFinished: {
        try {
          root.sessionLockXray = JSON.parse(String(sessionLockXrayOut.text || "{}")).bool === true
        } catch (e) {
          root.sessionLockXray = false
        }
      }
    }
  }

  Process {
    id: strandedLockCheckProc
    command: ["bash", "-c", "omarchy-hyprland-session-locked"]
    onExited: function(exitCode) {
      // No output to read the lock off yet.
      if (exitCode === 2) return

      root.strandedLockResolved = true

      // A lock taken while this was in flight is this shell's own.
      root.strandedLock = exitCode === 0 && !root.locked && !root.lockRequested
      root.recoverStrandedLock()
    }
  }

  Process {
    id: wakeProcess
    command: ["bash", "-c", "rm -f \"$1/display-off\"; main_mon=$(hyprctl monitors -j 2>/dev/null | jq -r '.[] | select(.x == 0 and .y == 0) | .name'); [[ -n $main_mon ]] && hyprctl repl \"hl.monitor({ output = '$main_mon', disabled = false })\" >/dev/null 2>&1; hyprctl dispatch 'hl.dsp.dpms({ action = \"enable\" })' >/dev/null 2>&1; omarchy-system-wake", "bash", root.blankMarkerDir]
  }

  Process {
    id: blankProcess
    // The marker goes down before the display does: the shell can be gone
    // within two seconds of the output dropping.
    command: ["bash", "-c", root.displayBlankingSuppressed
      ? "omarchy-brightness-keyboard off"
      : "mkdir -p \"$1\" && : > \"$1/display-off\"; omarchy-brightness-keyboard off; main_mon=$(hyprctl monitors -j 2>/dev/null | jq -r '.[] | select(.x == 0 and .y == 0) | .name'); [[ -n $main_mon ]] && hyprctl repl \"hl.monitor({ output = '$main_mon', disabled = true })\" >/dev/null 2>&1",
      "bash", root.blankMarkerDir]
  }

  // Runs once at startup. A display-off marker still there means the last
  // shell never reached a wake; it is kept as blank-crashed so every later
  // relaunch in this login sees it too (the runtime dir goes with the login).
  Process {
    id: blankCrashCheckProc
    command: ["bash", "-c",
      "[[ -e $1/display-off ]] && mv -f \"$1/display-off\" \"$1/blank-crashed\"; [[ -e $1/blank-crashed ]]",
      "bash", root.blankMarkerDir]
    onExited: function(exitCode) {
      if (exitCode !== 0) return
      root.blankCrashed = true
      root.logEvent("blank-crashed: keeping displays on for this login")
    }
  }

  Timer {
    id: idleBlankTimer
    interval: root.blankDelay
    repeat: false
    property double armedAt: 0
    onTriggered: {
      // A countdown frozen by suspend fires right after resume, which would
      // blank the freshly woken unlock screen under the user. Wall-clock time
      // exposes the gap: take a fresh run-up instead of blanking.
      if (Date.now() - armedAt > interval + 2000) {
        root.armBlankTimer()
        return
      }
      // Only a password check in flight should hold the display up. The
      // fingerprint PAM stays armed for the whole lock, so gating on
      // `authenticating` here would keep the panel lit until unlock.
      if (root.lockRequested && !root.authenticatingPassword) root.runBlank()
    }
  }

  Timer {
    id: sessionLockStabilizeTimer
    interval: 500
    repeat: false
    onTriggered: root.requestSessionLock()
  }

  Timer {
    id: pendingSessionLockTimer
    interval: 100
    repeat: true
    onTriggered: root.requestSessionLock()
  }

  Timer {
    id: strandedLockRetryTimer
    interval: 500
    repeat: true
    // Covers the compositor settling; screens coming back re-arm it.
    readonly property int budget: 20
    property int remaining: 20
    running: !root.strandedLockResolved && remaining > 0

    function rearm() {
      if (!root.strandedLockResolved) remaining = budget
    }

    onTriggered: {
      remaining -= 1
      root.checkStrandedLock()
    }
  }

  Connections {
    target: Quickshell
    function onScreensChanged() {
      root.requestSessionLock()

      // A monitor still coming up has no workspace, so cannot answer yet.
      strandedLockRetryTimer.rearm()
      root.checkStrandedLock()
    }
  }

  onAuthenticatingPasswordChanged: {
    if (!lockRequested) return
    if (authenticatingPassword) idleBlankTimer.stop()
    else armBlankTimer()
  }

  FileView {
    path: "/etc/pam.d/omarchy-lock-password"
    watchChanges: true
    printErrors: false
    onLoaded: root.passwordPamConfigured = true
    onLoadFailed: root.passwordPamConfigured = false
    onFileChanged: reload()
  }

  // No lock before PAM is known good. An answer from before then may be stale --
  // the failsafe can be cleared from a TTY -- so re-ask rather than act on it.
  onPasswordPamConfiguredChanged: {
    if (!passwordPamConfigured) return

    strandedLock = false
    strandedLockResolved = false
    strandedLockRetryTimer.rearm()
    checkStrandedLock()
  }

  // ------------------------------------------------- explorer facade
  // What the explorer, editor and designer get to see of this service on
  // Omarchy 4.0.3+, where the host keeps authentication services private
  // and hands the overlay `service = null` (issue #16). Published through
  // Bridge.js; it has no QObject parent and no reference back to this
  // object, so nothing here leads to the PAM contexts or the typed
  // password. extras/test-service-api.py keeps it in step with the UI.
  property var explorerApi: null

  Component {
    id: explorerApiComponent
    QtObject {
      readonly property string designId: root.designId
      readonly property int designsRevision: root.designsRevision
      readonly property string backgroundPath: root.backgroundPath
      readonly property int backgroundVersion: root.backgroundVersion
      readonly property string avatarPath: root.avatarPath
      readonly property int avatarVersion: root.avatarVersion
      readonly property string avatarUrl: root.avatarUrl
      readonly property string videoPath: root.videoPath
      readonly property string stingPath: root.stingPath
      readonly property int stingVolume: root.stingVolume
      readonly property real clipSpeed: root.clipSpeed
      readonly property bool clipWallpaper: root.clipWallpaper
      readonly property bool twelveHour: root.twelveHour
      readonly property string inputMonitor: root.inputMonitor
      readonly property string unlockAnimation: root.unlockAnimation
      readonly property int unlockDuration: root.unlockDuration
      readonly property var unlockAnimations: root.unlockAnimations
      readonly property int defaultUnlockDuration: root.defaultUnlockDuration
      readonly property int blankDelay: root.blankDelay
      readonly property int defaultBlankDelay: root.defaultBlankDelay
      readonly property bool keepDisplayOn: root.keepDisplayOn
      readonly property bool displayBlankingSuppressed: root.displayBlankingSuppressed
      readonly property bool fingerprintConfigured: root.fingerprintConfigured
      readonly property bool faceConfigured: root.faceConfigured
      readonly property bool fido2Configured: root.fido2Configured
      readonly property bool fido2Installed: root.fido2Installed
      readonly property bool fido2Enabled: root.fido2Enabled
      readonly property bool multimediaAvailable: root.multimediaAvailable
      readonly property bool menuEntryInstalled: root.menuEntryInstalled
      readonly property var components: root.components
      readonly property bool locked: root.locked
      readonly property bool previewVisible: root.previewVisible
      readonly property bool stingPlaying: root.stingPlaying
      readonly property string bootSetting: root.bootSetting
      readonly property bool bootApplying: root.bootApplying
      readonly property string bootApplied: root.bootApplied
      readonly property int bootAppliedVersion: root.bootAppliedVersion
      readonly property string bootAppliedTheme: root.bootAppliedTheme
      readonly property string bootCurrentTheme: root.bootCurrentTheme
      readonly property bool bootResync: root.bootResync
      readonly property int bootPreviewsVersion: root.bootPreviewsVersion
      readonly property bool bootPreviewsRunning: root.bootPreviewsRunning
      readonly property bool bootPreviewsPending: root.bootPreviewsPending
      readonly property var bootVideos: root.bootVideos
      readonly property var bootCustomDesigns: root.bootCustomDesigns
      readonly property int bootClipSeconds: root.bootClipSeconds
      readonly property var bootRotation: root.bootRotation

      signal designCustomized(string id, string path)
      signal designerDesignCreated(string id, string path)
      signal imagePicked(string path)
      signal clipDesignAdded(string id)
      signal bootResnapshotRequested(string designId, bool persist)
      signal exploreTabRequested(string tab)
      signal bootDesignLoaded(string name, string content)

      function setDesign(id) { return root.setDesign(id) }
      function customizeDesign(id) { return root.customizeDesign(id) }
      function createDesignerDesign(content) { return root.createDesignerDesign(content) }
      function deleteDesign(id) { return root.deleteDesign(id) }
      function rescanUserDesigns() { return root.rescanUserDesigns() }
      function reloadDesigns() { return root.reloadDesigns() }
      function rescanComponents() { return root.rescanComponents() }
      function saveComponent(slug, json) { return root.saveComponent(slug, json) }
      function deleteComponent(slug) { return root.deleteComponent(slug) }
      function pickAvatar(reopenExplorer) { return root.pickAvatar(reopenExplorer) }
      function setAvatar(path) { return root.setAvatar(path) }
      function clearAvatar() { return root.clearAvatar() }
      function resetAvatar() { return root.resetAvatar() }
      function pickImage(reopenExplorer) { return root.pickImage(reopenExplorer) }
      function pickVideo(reopenExplorer, target) { return root.pickVideo(reopenExplorer, target) }
      function setVideo(path) { return root.setVideo(path) }
      function clearVideo() { return root.clearVideo() }
      function setSting(path) { return root.setSting(path) }
      function clearSting() { return root.clearSting() }
      function setStingVolume(value) { return root.setStingVolume(value) }
      function playSting() { return root.playSting() }
      function endSting() { return root.endSting() }
      function createClipDesign(path) { return root.createClipDesign(path) }
      function setClipSpeed(v) { return root.setClipSpeed(v) }
      function setClipWallpaper(on) { return root.setClipWallpaper(on) }
      function setTwelveHour(v) { return root.setTwelveHour(v) }
      function setInputMonitor(name) { return root.setInputMonitor(name) }
      function setUnlockAnimation(name) { return root.setUnlockAnimation(name) }
      function setUnlockDuration(ms) { return root.setUnlockDuration(ms) }
      function setBlankDelay(ms) { return root.setBlankDelay(ms) }
      function setKeepDisplayOn(on) { return root.setKeepDisplayOn(on) }
      function refreshBackground() { return root.refreshBackground() }
      function refreshFingerprintStatus() { return root.refreshFingerprintStatus() }
      function refreshFaceStatus() { return root.refreshFaceStatus() }
      function refreshFido2Status() { return root.refreshFido2Status() }
      function setFido2Enabled(v) { return root.setFido2Enabled(v) }
      function refreshMenuEntry() { return root.refreshMenuEntry() }
      function setMenuEntry(v) { return root.setMenuEntry(v) }
      function logEvent(event) { return root.logEvent(event) }
      function setBoot(value) { return root.setBoot(value) }
      function applyBoot(force, explicitTarget) { return root.applyBoot(force, explicitTarget) }
      function applyBootSnapshot(id, persist, entryRect) { return root.applyBootSnapshot(id, persist, entryRect) }
      function setBootResync(on) { return root.setBootResync(on) }
      function refreshBootPreviews() { return root.refreshBootPreviews() }
      function setBootClipSeconds(n) { return root.setBootClipSeconds(n) }
      function loadBootDesign(name) { return root.loadBootDesign(name) }
      function saveBootDesign(name, content) { return root.saveBootDesign(name, content) }
      function createBootDesign() { return root.createBootDesign() }
      function deleteBootItem(id) { return root.deleteBootItem(id) }
      function toggleBootRotation(id) { return root.toggleBootRotation(id) }
      function enableBootRotation() { return root.enableBootRotation() }
    }
  }

  function publishExplorerApi() {
    if (explorerApi) return
    var api = explorerApiComponent.createObject(null)
    if (!api) return
    root.designCustomized.connect(api.designCustomized)
    root.designerDesignCreated.connect(api.designerDesignCreated)
    root.imagePicked.connect(api.imagePicked)
    root.clipDesignAdded.connect(api.clipDesignAdded)
    root.bootResnapshotRequested.connect(api.bootResnapshotRequested)
    root.exploreTabRequested.connect(api.exploreTabRequested)
    root.bootDesignLoaded.connect(api.bootDesignLoaded)
    explorerApi = api
    Bridge.publish(api)
  }

  function retireExplorerApi() {
    if (!explorerApi) return
    var api = explorerApi
    explorerApi = null
    Bridge.unpublish(api)
    api.destroy()
  }

  Component.onCompleted: {
    publishExplorerApi()
    refreshMenuEntry()
    refreshBackground()
    refreshFingerprintStatus()
    refreshSessionLockXray()
    rescanUserDesigns()
    detectAvatar()
    blankCrashCheckProc.running = true
    checkStrandedLock()
  }

  Component.onDestruction: retireExplorerApi()

  IpcHandler {
    target: "lock"

    function lock(): string {
      if (!root.passwordPamConfigured) return "missing-pam"
      if (!root.locked && !root.beginLock()) return "failed"
      return "ok"
    }

    function isLocked(): string {
      return root.locked ? "true" : "false"
    }

    function status(): string {
      return JSON.stringify({
        locked: root.locked,
        requested: root.lockRequested,
        pending: root.pendingSessionLock,
        sessionLocked: sessionLock.locked,
        secure: sessionLock.secure,
        realScreens: root.realScreenCount(),
        passwordPam: root.passwordPamConfigured,
        multimedia: root.multimediaAvailable,
        fingerprint: root.fingerprintConfigured,
        fingerprintConfigured: root.fingerprintConfigured,
        faceConfigured: root.faceConfigured,
        faceAuthenticating: root.faceAuthenticating,
        fido2Configured: root.fido2Configured,
        fido2Installed: root.fido2Installed,
        fido2Enabled: root.fido2Enabled,
        fido2Token: root.fido2TokenPresent,
        fido2Authenticating: root.fido2Authenticating,
        fido2PinAttempts: root.fido2PinAttempts,
        authMode: root.authMode,
        authenticating: root.authenticating,
        lastEvent: root.lastEvent,
        lastEventAt: root.lastEventAt,
        design: root.designId,
        boot: root.bootSetting,
        bootApplied: root.bootApplied,
        bootApplying: root.bootApplying,
        unlock: root.unlockAnimation,
        unlockMs: root.unlockDuration,
        unlockAnimated: root.unlockAnimated,
        blankMs: root.blankDelay,
        keepDisplayOn: root.keepDisplayOn,
        displayBlankingSuppressed: root.displayBlankingSuppressed,
        unlocking: root.unlocking,
        clipDesign: root.designHasClip,
        clipUnlocking: root.clipUnlocking,
        video: root.videoPath,
        sting: root.stingPath,
        stingPlaying: root.stingPlaying,
        previewTyped: root.previewTyped.length
      })
    }

    function preview(): string {
      root.previewUnlocking = false
      root.previewClipPlaying = false
      root.refreshBackground()
      root.refreshFingerprintStatus()
      root.previewVisible = true
      return "ok"
    }

    function hidePreview(): string {
      previewUnlockTimer.stop()
      root.previewUnlocking = false
      root.previewClipPlaying = false
      root.previewVisible = false
      root.previewDesignId = ""
      root.previewTyped = ""
      return "ok"
    }

    function design(): string {
      return root.designId
    }

    function designs(): string {
      return JSON.stringify(Designs.all().map(function(d) {
        return { id: d.id, name: d.name, description: d.description, active: d.id === root.designId }
      }))
    }

    function setDesign(id: string): string {
      return root.setDesign(id) ? "ok" : "unknown-design"
    }

    function setKeepDisplayOn(value: string): string {
      return root.setKeepDisplayOn(value) ? "ok" : "invalid-value"
    }

    function setBlankDelay(value: string): string {
      return root.setBlankDelay(value) ? "ok" : "invalid-value"
    }

    function boot(): string {
      var applied = root.bootApplied.length > 0 ? root.bootApplied : "stock"
      return root.bootSetting + " (applied: " + applied + (root.bootApplying ? ", rebuilding" : "") + ")"
    }

    function setBoot(value: string): string {
      if (root.bootApplying) return "busy"
      return root.setBoot(value) ? "ok" : "unknown-boot"
    }

    function setBootResync(value: string): string {
      return root.setBootResync(value === "on" || value === "true") ? "ok" : "failed"
    }

    function bootRotation(): string {
      return (root.bootRotation || []).join(",")
    }

    function toggleBootRotation(id: string): string {
      return root.toggleBootRotation(id) ? "ok" : "failed"
    }

    function enableBootRotation(): string {
      root.enableBootRotation()
      return "ok"
    }

    function setBootClipSeconds(value: string): string {
      return root.setBootClipSeconds(value) ? "ok" : "failed"
    }

    function previewDesign(id: string): string {
      root.rescanUserDesigns()
      if (!Designs.byId(String(id || ""))) return "unknown-design"
      root.previewDesignId = String(id)
      root.previewUnlocking = false
      root.refreshBackground()
      root.refreshFingerprintStatus()
      root.previewVisible = true
      return "ok"
    }

    function monitors(): string {
      var screens = Quickshell.screens || []
      return JSON.stringify(screens.map(function(s) { return { name: s.name, width: s.width, height: s.height, input: root.showsInput(s) } }))
    }

    function inputMonitor(): string {
      return root.inputMonitor
    }

    function setInputMonitor(name: string): string {
      return root.setInputMonitor(name) ? "ok" : "failed"
    }

    function unlockAnimation(): string {
      return root.unlockAnimated ? root.unlockAnimation + " " + root.unlockDuration + "ms" : "none"
    }

    function setUnlockAnimation(name: string): string {
      return root.setUnlockAnimation(name) ? "ok" : "unknown-animation"
    }

    function setUnlockDuration(ms: string): string {
      return root.setUnlockDuration(ms) ? "ok" : "out-of-range"
    }

    function previewUnlock(): string {
      if (!root.previewVisible) return "no-preview"
      var pd = Designs.byId(root.previewDesignId.length > 0 ? root.previewDesignId : root.designId)
      if (pd && pd.clip) {
        root.previewClipPlaying = true
        return "ok"
      }
      if (!root.unlockAnimated) {
        root.previewVisible = false
        root.previewDesignId = ""
        root.previewTyped = ""
        return "ok"
      }
      root.previewUnlocking = true
      previewUnlockTimer.restart()
      return "ok"
    }

    // Feeds the preview's password field, so a demo can type without hands.
    function previewType(text: string): string {
      if (!root.previewVisible) return "no-preview"
      root.previewTyped = String(text || "")
      return "ok"
    }

    function previewFail(): string {
      root.previewFailure = ""
      root.previewFailure = "Authentication failed (1)"
      previewFailureTimer.restart()
      return "ok"
    }

    function customize(id: string): string {
      return root.customizeDesign(id) ? "ok" : "unknown-design"
    }

    function editDesign(id: string): string {
      var d = Designs.byId(String(id || ""))
      if (!d || !d.path) return "not-a-custom-design"
      Quickshell.execDetached(["omarchy-launch-editor", decodeURIComponent(d.path.replace(/^file:\/\//, ""))])
      return "ok"
    }

    function reloadDesigns(): string {
      root.reloadDesigns()
      return "ok"
    }

    function rescanDesigns(): string {
      root.rescanUserDesigns()
      return "ok"
    }

    function rescanComponents(): string {
      root.rescanComponents()
      return "ok"
    }

    function components(): string {
      var names = []
      for (var i = 0; i < root.components.length; i++) names.push(root.components[i].comp.name)
      return names.length > 0 ? names.join("\n") : "none"
    }

    function avatar(): string {
      return root.avatarPath
    }

    function setAvatar(path: string): string {
      return root.setAvatar(path) ? "ok" : "failed"
    }

    function clearAvatar(): string {
      return root.clearAvatar() ? "ok" : "failed"
    }

    function resetAvatar(): string {
      return root.resetAvatar() ? "ok" : "failed"
    }

    function pickAvatar(): string {
      return root.pickAvatar(false) ? "ok" : "busy"
    }

    function video(): string {
      return root.videoPath.length > 0 ? root.videoPath : "none"
    }

    function setVideo(path: string): string {
      return root.setVideo(path) ? "ok" : "failed"
    }

    function clearVideo(): string {
      return root.clearVideo() ? "ok" : "failed"
    }

    function pickVideo(): string {
      return root.pickVideo(false, "video") ? "ok" : "busy"
    }

    function sting(): string {
      return root.stingPath.length > 0 ? root.stingPath : "none"
    }

    function setSting(path: string): string {
      return root.setSting(path) ? "ok" : "failed"
    }

    function clearSting(): string {
      return root.clearSting() ? "ok" : "failed"
    }

    function pickSting(): string {
      return root.pickVideo(false, "sting") ? "ok" : "busy"
    }

    function newClipDesign(): string {
      return root.pickVideo(false, "clip") ? "ok" : "busy"
    }

    function clipWallpaper(): string {
      return root.clipWallpaper ? "on" : "off"
    }

    function setClipWallpaper(on: string): string {
      return root.setClipWallpaper(on) ? "ok" : "failed"
    }

    function clipSpeed(): string {
      return String(root.clipSpeed)
    }

    function setClipSpeed(v: string): string {
      return root.setClipSpeed(v) ? "ok" : "failed"
    }

    function menuEntry(): string {
      return root.menuEntryInstalled ? "on" : "off"
    }

    function setMenuEntry(v: string): string {
      return root.setMenuEntry(v === "on" || v === "true" || v === "1") ? "ok" : "failed"
    }

    function clockFormat(): string {
      return root.twelveHour ? "12" : "24"
    }

    function setClockFormat(v: string): string {
      return root.setTwelveHour(v === "12" || v === "12h" || v === "true" || v === "on") ? "ok" : "failed"
    }

    function createClipDesign(path: string): string {
      return root.createClipDesign(path) ? "ok" : "failed"
    }

    function deleteDesign(id: string): string {
      return root.deleteDesign(id) ? "ok" : "failed"
    }

    function deleteBootItem(id: string): string {
      return root.deleteBootItem(id) ? "ok" : "failed"
    }

    function stingVolume(): string {
      return String(root.stingVolume)
    }

    function setStingVolume(value: string): string {
      return root.setStingVolume(value) ? "ok" : "failed"
    }

    function previewSting(): string {
      if (root.stingPath.length === 0) return "no-clip"
      return root.playSting() ? "ok" : "busy"
    }

    function explore(): string {
      root.rescanUserDesigns()
      if (root.shell && typeof root.shell.summon === "function")
        return root.shell.summon(root.pluginId, "{}") ? "ok" : "failed"
      return "no-shell"
    }

    // Open the built-in editor on a custom boot layout.
    function editBootLayout(name: string): string {
      root.loadBootDesign(String(name || ""))
      if (root.shell && typeof root.shell.summon === "function")
        return root.shell.summon(root.pluginId, "{}") ? "ok" : "failed"
      return "no-shell"
    }

    // Open the explorer on a specific tab: styling, animation, boot or
    // settings. Also handy for scripting and screenshots.
    function exploreTab(tab: string): string {
      root.rescanUserDesigns()
      root.exploreTabRequested(String(tab || "styling"))
      if (root.shell && typeof root.shell.summon === "function")
        return root.shell.summon(root.pluginId, "{}") ? "ok" : "failed"
      return "no-shell"
    }
  }
}
